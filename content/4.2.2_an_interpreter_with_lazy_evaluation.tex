\subsection{An Interpreter with Lazy Evaluation}
\label{Section 4.2.2}

In this section we will implement a normal-order language that is the same as Scheme except that compound procedures are non-strict in each argument.
Primitive procedures will still be strict.
It is not difficult to modify the evaluator of \link{Section 4.1.1} so that the language it interprets behaves this way.
Almost all the required changes center around procedure application.

The basic idea is that, when applying a procedure, the interpreter must determine which arguments are to be evaluated and which are to be delayed.
The delayed arguments are not evaluated;
instead, they are transformed into objects called \newterm{thunks}.%
\footnote{
	The word \newterm{thunk} was invented by an informal working group that was discussing the implementation of call-by-name in Algol~60.
	They observed that most of the analysis of (“thinking about”) the expression could be done at compile time;
	thus, at run time, the expression would already have been “thunk” about (\link{Ingerman et al. 1960}).
}
The thunk must contain the information required to produce the value of the argument when it is needed, as if it had been evaluated at the time of the application.
Thus, the thunk must contain the argument expression and the environment in which the procedure application is being evaluated.

The process of evaluating the expression in a thunk is called \newterm{forcing}.%
\footnote{
	This is analogous to the use of \code{force} on the delayed objects that were introduced in \link{Chapter 3} to represent streams.
	The critical difference between what we are doing here and what we did in \link{Chapter 3} is that we are building delaying and forcing into the evaluator, and thus making this uniform and automatic throughout the language.
}
In general, a thunk will be forced only when its value is needed:
when it is passed to a primitive procedure that will use the value of the thunk;
when it is the value of a predicate of a conditional;
and when it is the value of an operator that is about to be applied as a procedure.
One design choice we have available is whether or not to \newterm{memoize} thunks, as we did with delayed objects in \link{Section 3.5.1}.
With memoization, the first time a thunk is forced, it stores the value that is computed.
Subsequent forcings simply return the stored value without repeating the computation.
We’ll make our interpreter memoize, because this is more efficient for many applications.
There are tricky considerations here, however.%
\footnote{
	Lazy evaluation combined with memoization is sometimes referred to as \newterm{call-by-need} argument passing, in contrast to \newterm{call-by-name} argument passing.
	(Call-by-name, introduced in Algol~60, is similar to non-memoized lazy evaluation.)
	As language designers, we can build our evaluator to memoize, not to memoize, or leave this an option for programmers (\link{Exercise 4.31}).
	As you might expect from \link{Chapter 3}, these choices raise issues that become both subtle and confusing in the presence of assignments.
	(See \link{Exercise 4.27} and \link{Exercise 4.29}.)
	An excellent article by \autocite{clinger_nondeterministic_call} attempts to clarify the multiple dimensions of confusion that arise here.
}



\subsubsection*{Modifying the evaluator}

The main difference between the lazy evaluator and the one in \link{Section 4.1} is in the handling of procedure applications in \code{eval} and \code{apply}.

The \code{application?} clause of \code{eval} becomes
\begin{scheme}
  ((application? exp)
   (apply (actual-value (operator exp) env)
          (operands exp)
          env))
\end{scheme}
This is almost the same as the \code{application?} clause of \code{eval} in \link{Section 4.1.1}.
For lazy evaluation, however, we call \code{apply} with the operand expressions, rather than the arguments produced by evaluating them.
Since we will need the environment to construct thunks if the arguments are to be delayed, we must pass this as well.
We still evaluate the operator, because \code{apply} needs the actual procedure to be applied in order to dispatch on its type (primitive versus compound) and apply it.

Whenever we need the actual value of an expression, we use
\begin{scheme}
  (define (actual-value exp env)
    (force-it (eval exp env)))
\end{scheme}
instead of just \code{eval}, so that if the expression’s value is a thunk, it will be forced.

Our new version of \code{apply} is also almost the same as the version in \link{Section 4.1.1}.
The difference is that \code{eval} has passed in unevaluated operand expressions:
For primitive procedures (which are strict), we evaluate all the arguments before applying the primitive;
for compound procedures (which are non-strict) we delay all the arguments before applying the procedure.
\begin{scheme}
  (define (apply procedure arguments env)
    (cond ((primitive-procedure? procedure)
           (apply-primitive-procedure
            procedure
            (list-of-arg-values arguments env)))   ~\textrm{; changed}~
          ((compound-procedure? procedure)
           (eval-sequence
            (procedure-body procedure)
            (extend-environment
             (procedure-parameters procedure)
             (list-of-delayed-args arguments env)  ~\textrm{; changed}~
             (procedure-environment procedure))))
          (else (error "Unknown procedure type: APPLY"
                       procedure))))
\end{scheme}
The procedures that process the arguments are just like \code{list-of-values} from \link{Section 4.1.1}, except that \code{list-of-delayed-args} delays the arguments instead of evaluating them, and \code{list-of-arg-values} uses \code{actual-value} instead of \code{eval}:
\begin{scheme}
  (define (list-of-arg-values exps env)
    (if (no-operands? exps)
        '()
        (cons (actual-value (first-operand exps)
                            env)
              (list-of-arg-values (rest-operands exps)
                                  env))))

  (define (list-of-delayed-args exps env)
    (if (no-operands? exps)
        '()
        (cons (delay-it (first-operand exps)
                        env)
              (list-of-delayed-args (rest-operands exps)
                                    env))))
\end{scheme}

The other place we must change the evaluator is in the handling of \code{if}, where we must use \code{actual-value} instead of \code{eval} to get the value of the predicate expression before testing whether it is true or false:
\begin{scheme}
  (define (eval-if exp env)
    (if (true? (actual-value (if-predicate exp) env))
        (eval (if-consequent exp) env)
        (eval (if-alternative exp) env)))
\end{scheme}

Finally, we must change the \code{driver-loop} procedure (\link{Section 4.1.4}) to use \code{actual-value} instead of \code{eval}, so that if a delayed value is propagated back to the read-eval-print loop, it will be forced before being printed.
We also change the prompts to indicate that this is the lazy evaluator:
\begin{scheme}
  (define input-prompt  ";;; L-Eval input:")
  (define output-prompt ";;; L-Eval value:")

  (define (driver-loop)
    (prompt-for-input input-prompt)
    (let ((input (read)))
      (let ((output
             (actual-value
              input the-global-environment)))
        (announce-output output-prompt)
        (user-print output)))
    (driver-loop))
\end{scheme}

With these changes made, we can start the evaluator and test it.  The
successful evaluation of the \code{try} expression discussed in
\link{Section 4.2.1} indicates that the interpreter is performing lazy evaluation:
\begin{scheme}
  (define the-global-environment (setup-environment))

  (driver-loop)

  ~\outprint{;;; L-Eval input:}~
  (define (try a b) (if (= a 0) 1 b))
  ~\outprint{;;; L-Eval value:}~
  ~\outprint{ok}~

  ~\outprint{;;; L-Eval input:}~
  (try 0 (/ 1 0))
  ~\outprint{;;; L-Eval value:}~
    ~\outprint{1}~
\end{scheme}



\subsubsection*{Representing thunks}

Our evaluator must arrange to create thunks when procedures are applied to arguments and to force these thunks later.
A thunk must package an expression together with the environment, so that the argument can be produced later.
To force the thunk, we simply extract the expression and environment from the thunk and evaluate the expression in the environment.
We use \code{actual-value} rather than \code{eval} so that in case the value of the expression is itself a thunk, we will force that, and so on, until we reach something that is not a thunk:
\begin{scheme}
  (define (force-it obj)
    (if (thunk? obj)
        (actual-value (thunk-exp obj) (thunk-env obj))
        obj))
\end{scheme}

One easy way to package an expression with an environment is to make a list containing the expression and the environment.
Thus, we create a thunk as follows:
\begin{scheme}
  (define (delay-it exp env)
    (list 'thunk exp env))

  (define (thunk? obj)
    (tagged-list? obj 'thunk))

  (define (thunk-exp thunk) (cadr  thunk))

  (define (thunk-env thunk) (caddr thunk))
\end{scheme}

Actually, what we want for our interpreter is not quite this, but rather thunks that have been memoized.
When a thunk is forced, we will turn it into an evaluated thunk by replacing the stored expression with its value and changing the \code{thunk} tag so that it can be recognized as already evaluated.%
\footnote{
	Notice that we also erase the \code{env} from the thunk once the expression’s value has been computed.
	This makes no difference in the values returned by the interpreter.
	It does help save space, however, because removing the reference from the thunk to the \code{env} once it is no longer needed allows this structure to be \newterm{garbage-collected} and its space recycled, as we will discuss in \link{Section 5.3}.

	Similarly, we could have allowed unneeded environments in the memoized delayed objects of \link{Section 3.5.1} to be garbage-collected, by having \code{memo-proc} do something like \code{(set! proc '())} to discard the procedure \code{proc} (which includes the environment in which the \code{delay} was evaluated) after storing its value.
}
\begin{scheme}
  (define (evaluated-thunk? obj)
    (tagged-list? obj 'evaluated-thunk))

  (define (thunk-value evaluated-thunk)
    (cadr evaluated-thunk))

  (define (force-it obj)
    (cond ((thunk? obj)
           (let ((result (actual-value (thunk-exp obj)
                                       (thunk-env obj))))
             (set-car! obj 'evaluated-thunk)
             (set-car! (cdr obj)
                       result)     ~\textrm{; replace \code{exp} with its value}~
             (set-cdr! (cdr obj)
                       '())        ~\textrm{; forget unneeded \code{env}}~
             result))
          ((evaluated-thunk? obj) (thunk-value obj))
          (else obj)))
\end{scheme}
Notice that the same \code{delay-it} procedure works both with and without
memoization.



\begin{exercise}
	\label{Exercise 4.27}
	Suppose we type in the following definitions to the lazy evaluator:
	\begin{scheme}
	  (define count 0)

	  (define (id x) (set! count (+ count 1)) x)
	\end{scheme}
	Give the missing values in the following sequence of interactions, and explain your answers.%
	\footnote{
		This exercise demonstrates that the interaction between lazy evaluation and side effects can be very confusing.
		This is just what you might expect from the discussion in \link{Chapter 3}.
	}
	\begin{scheme}
	  (define w (id (id 10)))

	  ~\outprint{;;; L-Eval input:}~
	  count
	  ~\outprint{;;; L-Eval value:}~
	  ⟨~\var{response}~⟩

	  ~\outprint{;;; L-Eval input:}~
	  w
	  ~\outprint{;;; L-Eval value:}~
	  ⟨~\var{response}~⟩

	  ~\outprint{;;; L-Eval input:}~
	  count
	  ~\outprint{;;; L-Eval value:}~
	  ⟨~\var{response}~⟩
	\end{scheme}
\end{exercise}



\begin{exercise}
	\label{Exercise 4.28}
	\code{eval} uses \code{actual-value} rather than \code{eval} to evaluate the operator before passing it to \code{apply}, in order to force the value of the operator.
	Give an example that demonstrates the need for this forcing.
\end{exercise}



\begin{exercise}
	\label{Exercise 4.29}
	Exhibit a program that you would expect to run much more slowly without memoization than with memoization.
	Also, consider the following interaction, where the \code{id} procedure is defined as in \link{Exercise 4.27} and \code{count} starts at \( 0 \):
	\begin{scheme}
	  (define (square x) (* x x))

	  ~\outprint{;;; L-Eval input:}~
	  (square (id 10))
	  ~\outprint{;;; L-Eval value:}~
	  ⟨~\var{response}~⟩

	  ~\outprint{;;; L-Eval input:}~
	  count
	  ~\outprint{;;; L-Eval value:}~
	  ⟨~\var{response}~⟩
	\end{scheme}
	Give the responses both when the evaluator memoizes and when it does not.
\end{exercise}



\begin{exercise}
	\label{Exercise 4.30}
	Cy~D.~Fect, a reformed C~programmer, is worried that some side effects may never take place, because the lazy evaluator doesn’t force the expressions in a sequence.
	Since the value of an expression in a sequence other than the last one is not used (the expression is there only for its effect, such as assigning to a variable or printing), there can be no subsequent use of this value (e.g., as an argument to a primitive procedure) that will cause it to be forced.
	Cy thus thinks that when evaluating sequences, we must force all expressions in the sequence except the final one.
	He proposes to modify \code{eval-sequence} from \link{Section 4.1.1} to use \code{actual-value} rather than \code{eval}:
	\begin{scheme}
	  (define (eval-sequence exps env)
	    (cond ((last-exp? exps) (eval (first-exp exps) env))
	          (else (actual-value (first-exp exps) env)
	                (eval-sequence (rest-exps exps) env))))
	\end{scheme}
	\begin{enumerate}[label = \alph*., leftmargin = *]

		\item
			Ben Bitdiddle thinks Cy is wrong.
			He shows Cy the \code{for-each} procedure described in \link{Exercise 2.23}, which gives an important example of a sequence with side effects:
			\begin{scheme}
			  (define (for-each proc items)
			    (if (null? items)
			        'done
			        (begin (proc (car items))
			               (for-each proc (cdr items)))))
			\end{scheme}
			He claims that the evaluator in the text (with the original \code{eval-sequence}) handles this correctly:
			\begin{scheme}
			  ~\outprint{;;; L-Eval input:}~
			  (for-each (lambda (x) (newline) (display x))
			            (list 57 321 88))
			  ~\outprint{57}~
			  ~\outprint{321}~
			  ~\outprint{88}~
			  ~\outprint{;;; L-Eval value:}~
			  ~\outprint{done}~
			\end{scheme}
			Explain why Ben is right about the behavior of \code{for-each}.

		\item
			Cy agrees that Ben is right about the \code{for-each} example, but says that that’s not the kind of program he was thinking about when he proposed his change to \code{eval-sequence}.
			He defines the following two procedures in the lazy evaluator:
			\begin{scheme}
			  (define (p1 x)
			    (set! x (cons x '(2)))
			    x)

			  (define (p2 x)
			    (define (p e)
			      e
			      x)
			    (p (set! x (cons x '(2)))))
			\end{scheme}
			What are the values of \code{(p1 1)} and \code{(p2 1)} with the original \code{eval-sequence}?
			What would the values be with Cy’s proposed change to \code{eval-sequence}?

		\item
			Cy also points out that changing \code{eval-sequence} as he proposes does not affect the behavior of the example in part a.
			Explain why this is true.

		\item
			How do you think sequences ought to be treated in the lazy evaluator?
			Do you like Cy’s approach, the approach in the text, or some other approach?

	\end{enumerate}
\end{exercise}



\begin{exercise}
	\label{Exercise 4.31}
	The approach taken in this section is somewhat unpleasant, because it makes an incompatible change to Scheme.
	It might be nicer to implement lazy evaluation as an \newterm{upward-compatible extension}, that is, so that ordinary Scheme programs will work as before.
	We can do this by extending the syntax of procedure declarations to let the user control whether or not arguments are to be delayed.
	While we’re at it, we may as well also give the user the choice between delaying with and without memoization.
	For example, the definition
	\begin{scheme}
	  (define (f a (b lazy) c (d lazy-memo))
	    …)
	\end{scheme}
	would define \code{f} to be a procedure of four arguments, where the first and third arguments are evaluated when the procedure is called, the second argument is delayed, and the fourth argument is both delayed and memoized.
	Thus, ordinary procedure definitions will produce the same behavior as ordinary Scheme, while adding the \code{lazy-memo} declaration to each parameter of every compound procedure will produce the behavior of the lazy evaluator defined in this section.
	Design and implement the changes required to produce such an extension to Scheme.
	You will have to implement new syntax procedures to handle the new syntax for \code{define}.
	You must also arrange for \code{eval} or \code{apply} to determine when arguments are to be delayed, and to force or delay arguments accordingly, and you must arrange for forcing to memoize or not, as appropriate.
\end{exercise}
