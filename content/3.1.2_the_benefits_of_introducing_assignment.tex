\subsection{The Benefits of Introducing Assignment}
\label{Section 3.1.2}

As we shall see, introducing assignment into our programming language leads us into a thicket of difficult conceptual issues.
Nevertheless, viewing systems as collections of objects with local state is a powerful technique for maintaining a modular design.
As a simple example, consider the design of a procedure \code{rand} that, whenever it is called, returns an integer chosen at random.

It is not at all clear what is meant by “chosen at random.”
What we presumably want is for successive calls to \code{rand} to produce a sequence of numbers that has statistical properties of uniform distribution.
We will not discuss methods for generating suitable sequences here.
Rather, let us assume that we have a procedure \code{rand-update} that has the property that if we start with a given number \( x_1 \) and form
\begin{scheme}
  ~\dark{\( x_2 \)}~ = (rand-update ~\dark{\( x_1 \)}~)
  ~\dark{\( x_3 \)}~ = (rand-update ~\dark{\( x_2 \)}~)
\end{scheme}
then the sequence of values \( x_1, x_2, x_3, \dotsc \) will have the
desired statistical properties.%
\footnote{
	One common way to implement \code{rand-update} is to use the rule that \( x \) is updated to \( a x + b \) modulo \( m \), where \( a \), \( b \), and \( m \) are appropriately chosen integers.
	Chapter 3 of \link{Knuth 1981} includes an extensive discussion of techniques for generating sequences of random numbers and establishing their statistical properties.
	Notice that the \code{rand-update} procedure computes a mathematical function:
	Given the same input twice, it produces the same output.
	Therefore, the number sequence produced by \code{rand-update} certainly is not “random,”  if by  “random” we insist that each number in the sequence is unrelated to the preceding number.
	The relation between “real randomness” and so-called \newterm{pseudo-random} sequences, which are produced by well-determined computations and yet have suitable statistical properties, is a complex question involving difficult issues in mathematics and philosophy.
	Kolmogorov, Solomonoff, and Chaitin have made great progress in clarifying these issues;
	a discussion can be found in \autocite{chaitin_randomness_proof}.
}

We can implement \code{rand} as a procedure with a local state variable \code{x} that is initialized to some fixed value \code{random-init}.
Each call to \code{rand} computes \code{rand-update} of the current value of \code{x}, returns this as the random number, and also stores this as the new value of \code{x}.
\begin{scheme}
  (define rand (let ((x random-init))
                 (lambda ()
                   (set! x (rand-update x))
                   x)))
\end{scheme}

Of course, we could generate the same sequence of random numbers without using assignment by simply calling \code{rand-update} directly.
However, this would mean that any part of our program that used random numbers would have to explicitly remember the current value of \code{x} to be passed as an argument to \code{rand-update}.
To realize what an annoyance this would be, consider using random numbers to implement a technique called \newterm{Monte Carlo simulation}.

The Monte Carlo method consists of choosing sample experiments at random from a large set and then making deductions on the basis of the probabilities estimated from tabulating the results of those experiments.
For example, we can approximate \( π \) using the fact that \( 6 / π^2 \) is the probability that two integers chosen at random will have no factors in common;
that is, that their greatest common divisor will be 1.%
\footnote{
	This theorem is due to E.~Cesàro.
	See section 4.5.2 of \link{Knuth 1981} for a discussion and a proof.
}
To obtain the approximation to \( π \), we perform a large number of experiments.
In each experiment we choose two integers at random and perform a test to see if their \acronym{GCD} is 1.
The fraction of times that the test is passed gives us our estimate of \( 6 / π^2 \), and from this we obtain our approximation to \( π \).

The heart of our program is a procedure \code{monte-carlo}, which takes as arguments the number of times to try an experiment, together with the experiment, represented as a no-argument procedure that will return either true or false each time it is run.
\code{monte-carlo} runs the experiment for the designated number of trials and returns a number telling the fraction of the trials in which the experiment was found to be true.
\begin{scheme}
  (define (estimate-pi trials)
    (sqrt (/ 6 (monte-carlo trials cesaro-test))))

  (define (cesaro-test)
     (= (gcd (rand) (rand)) 1))

  (define (monte-carlo trials experiment)
    (define (iter trials-remaining trials-passed)
      (cond ((= trials-remaining 0)
             (/ trials-passed trials))
            ((experiment)
             (iter (- trials-remaining 1)
                   (+ trials-passed 1)))
            (else
             (iter (- trials-remaining 1)
                   trials-passed))))
    (iter trials 0))
\end{scheme}

Now let us try the same computation using \code{rand-update} directly rather than \code{rand}, the way we would be forced to proceed if we did not use assignment to model local state:

\begin{scheme}
  (define (estimate-pi trials)
    (sqrt (/ 6 (random-gcd-test trials random-init))))

  (define (random-gcd-test trials initial-x)
    (define (iter trials-remaining trials-passed x)
      (let ((x1 (rand-update x)))
        (let ((x2 (rand-update x1)))
          (cond ((= trials-remaining 0)
                 (/ trials-passed trials))
                ((= (gcd x1 x2) 1)
                 (iter (- trials-remaining 1)
                       (+ trials-passed 1)
                       x2))
                (else
                 (iter (- trials-remaining 1)
                       trials-passed
                       x2))))))
    (iter trials 0 initial-x))
\end{scheme}

While the program is still simple, it betrays some painful breaches of modularity.
In our first version of the program, using \code{rand}, we can express the Monte Carlo method directly as a general \code{monte-carlo} procedure that takes as an argument an arbitrary \code{experiment} procedure.
In our second version of the program, with no local state for the random-number generator, \code{random-gcd-test} must explicitly manipulate the random numbers \code{x1} and \code{x2} and recycle \code{x2} through the iterative loop as the new input to \code{rand-update}.
This explicit handling of the random numbers intertwines the structure of accumulating test results with the fact that our particular experiment uses two random numbers, whereas other Monte Carlo experiments might use one random number or three.
Even the top-level procedure \code{estimate-pi} has to be concerned with supplying an initial random number.
The fact that the random-number generator’s insides are leaking out into other parts of the program makes it difficult for us to isolate the Monte Carlo idea so that it can be applied to other tasks.
In the first version of the program, assignment encapsulates the state of the random-number generator within the \code{rand} procedure, so that the details of random-number generation remain independent of the rest of the program.

The general phenomenon illustrated by the Monte Carlo example is this:
From the point of view of one part of a complex process, the other parts appear to change with time.
They have hidden time-varying local state.
If we wish to write computer programs whose structure reflects this decomposition, we make computational objects (such as bank accounts and random-number generators) whose behavior changes with time.
We model state with local state variables, and we model the changes of state with assignments to those variables.

It is tempting to conclude this discussion by saying that, by introducing assignment and the technique of hiding state in local variables, we are able to structure systems in a more modular fashion than if all state had to be manipulated explicitly, by passing additional parameters.
Unfortunately, as we shall see, the story is not so simple.

\begin{exercise}
	\label{Exercise 3.5}
	\newterm{Monte Carlo integration} is a method of estimating definite integrals by means of Monte Carlo simulation.
	Consider computing the area of a region of space described by a predicate \( P(x, y) \) that is true for points \( (x, y) \) in the region and false for points not in the region.
	For example, the region contained within a circle of radius 3 centered at (5, 7) is described by the predicate that tests whether \( (x - 5)^2 + (y - 7)^2 ≤ 3^2 \).
	To estimate the area of the region described by such a predicate, begin by choosing a rectangle that contains the region.
	For example, a rectangle with diagonally opposite corners at \( (2, 4) \) and \( (8, 10) \) contains the circle above.
	The desired integral is the area of that portion of the rectangle that lies in the region.
	We can estimate the integral by picking, at random, points \( (x, y) \) that lie in the rectangle, and testing \( P(x, y) \) for each point to determine whether the point lies in the region.
	If we try this with many points, then the fraction of points that fall in the region should give an estimate of the proportion of the rectangle that lies in the region.
	Hence, multiplying this fraction by the area of the entire rectangle should produce an estimate of the integral.

	Implement Monte Carlo integration as a procedure \code{estimate-integral} that takes as arguments a predicate \code{P}, upper and lower bounds \code{x1}, \code{x2}, \code{y1}, and \code{y2} for the rectangle, and the number of trials to perform in order to produce the estimate.
	Your procedure should use the same \code{monte-carlo} procedure that was used above to estimate \( π \).
	Use your \code{estimate-integral} to produce an estimate of \( π \) by measuring the area of a unit circle.

	You will find it useful to have a procedure that returns a number chosen at random from a given range.
	The following \code{random-in-range} procedure implements this in terms of the \code{random} procedure used in \link{Section 1.2.6}, which returns a nonnegative number less than its input.%
	\footnote{
		\acronym{MIT} Scheme provides such a procedure.
		If \code{random} is given an exact integer (as in \link{Section 1.2.6}) it returns an exact integer, but if it is given a decimal value (as in this exercise) it returns a decimal value.
	}
	\begin{scheme}
	  (define (random-in-range low high)
	    (let ((range (- high low)))
	      (+ low (random range))))
	\end{scheme}
\end{exercise}



\begin{exercise}
	\label{Exercise 3.6}
	It is useful to be able to reset a random-number generator to produce a sequence starting from a given value.
	Design a new \code{rand} procedure that is called with an argument that is either the symbol \code{generate} or the symbol \code{reset} and behaves as follows:
	\code{(rand 'generate)} produces a new random number;
	\code{((rand 'reset) ⟨\var{new-value}⟩)} resets the internal state variable to the designated \code{⟨\var{new-value}⟩}.
	Thus, by resetting the state, one can generate repeatable sequences.
	These are very handy to have when testing and debugging programs that use random numbers.
\end{exercise}
