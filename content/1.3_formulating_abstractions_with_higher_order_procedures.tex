\section{Formulating Abstractions with Higher-Order Procedures}
\label{Section 1.3}

We have seen that procedures are, in effect, abstractions that describe compound operations on numbers independent of the particular numbers.
For example, when we
\begin{scheme}
  (define (cube x) (* x x x))
\end{scheme}
we are not talking about the cube of a particular number, but rather about a method for obtaining the cube of any number.
Of course we could get along without ever defining this procedure, by always writing expressions such as
\begin{scheme}
  (* 3 3 3)
  (* x x x)
  (* y y y)
\end{scheme}
and never mentioning \code{cube} explicitly.
This would place us at a serious disadvantage, forcing us to work always at the level of the particular operations that happen to be primitives in the language (multiplication, in this case) rather than in terms of higher-level operations.
Our programs would be able to compute cubes, but our language would lack the ability to express the concept of cubing.
One of the things we should demand from a powerful programming language is the ability to build abstractions by assigning names to common patterns and then to work in terms of the abstractions directly.
Procedures provide this ability.
This is why all but the most primitive programming languages include mechanisms for defining procedures.

Yet even in numerical processing we will be severely limited in our ability to create abstractions if we are restricted to procedures whose parameters must be numbers.
Often the same programming pattern will be used with a number of different procedures.
To express such patterns as concepts, we will need to construct procedures that can accept procedures as arguments or return procedures as values.
Procedures that manipulate procedures are called \newterm{higher-order procedures}.
This section shows how higher-order procedures can serve as powerful abstraction mechanisms, vastly increasing the expressive power of our language.



\subsection{Procedures as Arguments}
\label{Section 1.3.1}

Consider the following three procedures.
The first computes the sum of the  integers from \code{a} through \code{b}:
\begin{scheme}
  (define (sum-integers a b)
    (if (> a b)
        0
        (+ a (sum-integers (+ a 1) b))))
\end{scheme}
The second computes the sum of the cubes of the integers in the given range:
\begin{scheme}
  (define (sum-cubes a b)
    (if (> a b)
        0
        (+ (cube a)
           (sum-cubes (+ a 1) b))))
\end{scheme}
The third computes the sum of a sequence of terms in the series
\[
	\frac{1}{1 ⋅ 3} + \frac{1}{5 ⋅ 7} + \frac{1}{9 ⋅ 11} + \dotsb
\]
which converges to \( π / 8 \) (very slowly):%
\footnote{
	This series, usually written in the equivalent form \( \frac{π}{4} = 1 - \frac{1}{3} + \frac{1}{5} - \frac{1}{7} + \dotsb \), is due to Leibniz.
	We’ll see how to use this as the basis for some fancy numerical tricks in \link{Section 3.5.3}.
}
\begin{scheme}
  (define (pi-sum a b)
    (if (> a b)
        0
        (+ (/ 1.0 (* a (+ a 2)))
           (pi-sum (+ a 4) b))))
\end{scheme}

These three procedures clearly share a common underlying pattern.
They are for the most part identical, differing only in the name of the procedure, the function of \code{a} used to compute the term to be added, and the function that provides the next value of \code{a}.
We could generate each of the procedures by filling in slots in the same template:
\begin{scheme}
  (define (⟨~\var{name}~⟩ a b)
    (if (> a b)
        0
        (+ (⟨~\var{term}~⟩ a)
           (⟨~\var{name}~⟩ (⟨~\var{next}~⟩ a) b))))
\end{scheme}

The presence of such a common pattern is strong evidence that there is a useful abstraction waiting to be brought to the surface.
Indeed, mathematicians long ago identified the abstraction of \newterm{summation of a series} and invented “sigma notation,” for example
\[
	∑_{n = a}^b f(n) = f(a) + \dotsb + f(b) \,,
\]
to express this concept.
The power of sigma notation is that it allows mathematicians to deal with the concept of summation itself rather than only with particular sums---for example, to formulate general results about sums that are independent of the particular series being summed.

Similarly, as program designers, we would like our language to be powerful enough so that we can write a procedure that expresses the concept of summation itself rather than only procedures that compute particular sums.
We can do so readily in our procedural language by taking the common template shown above and transforming the “slots” into formal parameters:
\begin{scheme}
  (define (sum term a next b)
    (if (> a b)
        0
        (+ (term a)
           (sum term (next a) next b))))
\end{scheme}
Notice that \code{sum} takes as its arguments the lower and upper bounds \code{a} and \code{b} together with the procedures \code{term} and \code{next}.
We can use \code{sum} just as we would any procedure.
For example, we can use it (along with a procedure \code{inc} that increments its argument by \( 1 \)) to define \code{sum-cubes}:
\begin{scheme}
  (define (inc n) (+ n 1))
  (define (sum-cubes a b)
    (sum cube a inc b))
\end{scheme}
Using this, we can compute the sum of the cubes of the integers from \( 1 \) to \( 10 \):
\begin{scheme}
  (sum-cubes 1 10)
  ~\outprint{3025}~
\end{scheme}
With the aid of an identity procedure to compute the term, we can define
\code{sum-integers} in terms of \code{sum}:
\begin{scheme}
  (define (identity x) x)

  (define (sum-integers a b)
    (sum identity a inc b))
\end{scheme}
Then we can add up the integers from \( 1 \) to \( 10 \):
\begin{scheme}
  (sum-integers 1 10)
  ~\outprint{55}~
\end{scheme}
We can also define \code{pi-sum} in the same way:%
\footnote{
	Notice that we have used block structure (\link{Section 1.1.8}) to embed the definitions of \code{pi-next} and \code{pi-term} within \code{pi-sum}, since these procedures are unlikely to be useful for any other purpose.
	We will see how to get rid of them altogether in \link{Section 1.3.2}.
}
\begin{scheme}
  (define (pi-sum a b)
    (define (pi-term x)
      (/ 1.0 (* x (+ x 2))))
    (define (pi-next x)
      (+ x 4))
    (sum pi-term a pi-next b))
\end{scheme}
Using these procedures, we can compute an approximation to \( π \):
\begin{scheme}
  (* 8 (pi-sum 1 1000))
  ~\outprint{3.139592655589783}~
\end{scheme}

Once we have \code{sum}, we can use it as a building block in formulating further concepts.
For instance, the definite integral of a function \( f \) between the limits \( a \) and \( b \) can be approximated numerically using the formula
\[
	\int_a^b f
	=
	\biggl[
		f\biggl( a + \frac{dx}{2} \biggr)
		+ f\biggl( a + dx + \frac{dx}{2} \biggr)
		+ f\biggl( a + 2 dx + \frac{dx}{2} \biggr)
		+ \dotsb
	\biggr]
	dx
\]
for small values of \( dx \).
We can express this directly as a procedure:
\begin{scheme}
  (define (integral f a b dx)
    (define (add-dx x)
      (+ x dx))
    (* (sum f (+ a (/ dx 2.0)) add-dx b)
       dx))

  (integral cube 0 1 0.01)
  ~\outprint{.24998750000000042}~

  (integral cube 0 1 0.001)
  ~\outprint{.249999875000001}~
\end{scheme}
(The exact value of the integral of \code{cube} between \( 0 \) and \( 1 \) is \( 1/4 \).)



\begin{exercise}
	\label{Exercise 1.29}
	Simpson’s Rule is a more accurate method of numerical integration than the method illustrated above.
	Using Simpson’s Rule, the integral of a function \( f \) between \( a \) and \( b \) is approximated as
	\[
		\frac{h}{3}
		(y_0 + 4 y_1 + 2 y_2 + 4 y_3 + 2 y_4 + \dotsb + 2 y_{n-2} + 4 y_{n-1} + y_n) \,, 
	\]
	where \( h = (b - a) / n \), for some even integer \( n \), and \( y_k = f(a + kh) \).
	(Increasing \( n \) increases the accuracy of the approximation.)
	Define a procedure that takes as arguments \( f \), \( a \), \( b \), and \( n \) and returns the value of the integral, computed using Simpson’s Rule.
	Use your procedure to integrate \code{cube} between \( 0 \) and \( 1 \) (with \( n = 100 \) and \( n = 1000 \)), and compare the results to those of the \code{integral} procedure shown above.
\end{exercise}



\begin{exercise}
	\label{Exercise 1.30}
	The \code{sum} procedure above generates a linear recursion.
	The procedure can be rewritten so that the sum is performed iteratively.
	Show how to do this by filling in the missing expressions in the following definition:
	\begin{scheme}
	  (define (sum term a next b)
	    (define (iter a result)
	      (if ⟨??⟩
	          ⟨??⟩
	          (iter ⟨??⟩ ⟨??⟩)))
	    (iter ⟨??⟩ ⟨??⟩))
	\end{scheme}
\end{exercise}



\begin{exercise}
	\label{Exercise 1.31}
	\begin{enumerate}[label = \alph*., leftmargin =  *]

		\item
			The \code{sum} procedure is only the simplest of a vast number of similar abstractions that can be captured as higher-order procedures.%
			\footnote{
				The intent of \link{Exercise 1.31} through \link{Exercise 1.33} is to demonstrate the expressive power that is attained by using an appropriate abstraction to consolidate many seemingly disparate operations.
				However, though accumulation and filtering are elegant ideas, our hands are somewhat tied in using them at this point since we do not yet have data structures to provide suitable means of combination for these abstractions.
				We will return to these ideas in \link{Section 2.2.3} when we show how to use \newterm{sequences} as interfaces for combining filters and accumulators to build even more powerful abstractions.
				We will see there how these methods really come into their own as a powerful and elegant approach to designing programs.
			}
			Write an analogous procedure called \code{product} that returns the product of the values of a function at points over a given range.
			Show how to define \code{factorial} in terms of \code{product}.
			Also use \code{product} to compute approximations to \( π \) using the formula%
			\footnote{
				This formula was discovered by the seventeenth-century English mathematician John Wallis.
			}
			\[
				\frac{π}{4}
				=
				\frac{2 ⋅ 4 ⋅ 4 ⋅ 6 ⋅ 6 ⋅ 8 \dotsm}{3 ⋅ 3 ⋅ 5 ⋅ 5 ⋅ 7 ⋅ 7 \dotsm}
			\]

		\item
			If your \code{product} procedure generates a recursive process, write one that generates an iterative process.
			If it generates an iterative process, write one that generates a recursive process.

	\end{enumerate}
\end{exercise}



\begin{exercise}
	\label{Exercise 1.32}
	\begin{enumerate}[label = \alph*., leftmargin = *]

		\item
			Show that \code{sum} and \code{product} (\link{Exercise 1.31}) are both special cases of a still more general notion called \code{accumulate} that combines a collection of terms, using some general accumulation function:
			\begin{scheme}
			  (accumulate combiner null-value term a next b)
			\end{scheme}

			\code{accumulate} takes as arguments the same term and range specifications as \code{sum} and \code{product}, together with a \code{combiner} procedure (of two arguments) that specifies how the current term is to be combined with the accumulation of the preceding terms and a \code{null-value} that specifies what base value to use when the terms run out.
			Write \code{accumulate} and show how \code{sum} and \code{product} can both be defined as simple calls to \code{accumulate}.

		\item
			If your \code{accumulate} procedure generates a recursive process, write one that generates an iterative process.
			If it generates an iterative process, write one that generates a recursive process.

	\end{enumerate}
\end{exercise}



\begin{exercise}
	\label{Exercise 1.33}
	You can obtain an even more general version of \code{accumulate} (\link{Exercise 1.32}) by introducing the notion of a \newterm{filter} on the terms to be combined.
	That is, combine only those terms derived from values in the range that satisfy a specified condition.
	The resulting \code{filtered-accumulate} abstraction takes the same arguments as accumulate, together with an additional predicate of one argument that specifies the filter.
	Write \code{filtered-accumulate} as a procedure.
	Show how to express the following using \code{filtered-accumulate}:
	\begin{enumerate}[label = \alph*., leftmargin = *]

		\item
			the sum of the squares of the prime numbers in the interval \( a \) to \( b \) (assuming that you have a \code{prime?} predicate already written).

		\item
			the product of all the positive integers less than \( n \) that are relatively prime to \( n \) (i.e., all positive integers \( i < n \) such that \( \gcd(i, n) = 1 \)).

	\end{enumerate}
\end{exercise}



\subsection{Constructing Procedures Using \code{lambda}}
\label{Section 1.3.2}

In using \code{sum} as in \link{Section 1.3.1}, it seems terribly awkward to have to define trivial procedures such as \code{pi-term} and \code{pi-next} just so we can use them as arguments to our higher-order procedure.
Rather than define \code{pi-next} and \code{pi-term}, it would be more convenient to have a way to directly specify “the procedure that returns its input incremented by 4”  and  “the procedure that returns the reciprocal of its input times its input plus 2.”
We can do this by introducing the special form \code{lambda}, which creates procedures.
Using \code{lambda} we can describe what we want as
\begin{scheme}
  (lambda (x) (+ x 4))
\end{scheme}
and
\begin{scheme}
  (lambda (x) (/ 1.0 (* x (+ x 2))))
\end{scheme}
Then our \code{pi-sum} procedure can be expressed without defining any auxiliary procedures as
\begin{scheme}
  (define (pi-sum a b)
    (sum (lambda (x) (/ 1.0 (* x (+ x 2))))
         a
         (lambda (x) (+ x 4))
         b))
\end{scheme}

Again using \code{lambda}, we can write the \code{integral} procedure without having to define the auxiliary procedure \code{add-dx}:
\begin{scheme}
  (define (integral f a b dx)
    (* (sum f
            (+ a (/ dx 2.0))
            (lambda (x) (+ x dx))
            b)
       dx))
\end{scheme}

In general, \code{lambda} is used to create procedures in the same way as \code{define}, except that no name is specified for the procedure:
\begin{scheme}
  (lambda (⟨~\var{\dark formal-parameters}~⟩) ⟨~\var{body}~⟩)
\end{scheme}
The resulting procedure is just as much a procedure as one that is created using \code{define}.
The only difference is that it has not been associated with any name in the environment.
In fact,
\begin{scheme}
  (define (plus4 x) (+ x 4))
\end{scheme}
is equivalent to
\begin{scheme}
  (define plus4 (lambda (x) (+ x 4)))
\end{scheme}
We can read a \code{lambda} expression as follows:
\begin{scheme}
  (lambda                     (x)     (+   x     4))
      |                        |       |   |     |
  the procedure of an argument x that adds x and 4
\end{scheme}

Like any expression that has a procedure as its value, a \code{lambda} expression can be used as the operator in a combination such as
\begin{scheme}
  ((lambda (x y z) (+ x y (square z)))
   1 2 3)
  ~\outprint{12}~
\end{scheme}
or, more generally, in any context where we would normally use a procedure name.%
\footnote{
	It would be clearer and less intimidating to people learning Lisp if a name more obvious than \code{lambda}, such as \code{make-procedure}, were used.
	But the convention is firmly entrenched.
	The notation is adopted from the λ-calculus, a mathematical formalism introduced by the mathematical logician Alonzo \link{Church (1941)}.
	Church developed the λ-calculus to provide a rigorous foundation for studying the notions of function and function application.
	The λ-calculus has become a basic tool for mathematical investigations of the semantics of programming languages.
}



\subsubsection*{Using \code{let} to create local variables}

Another use of \code{lambda} is in creating local variables.
We often need local variables in our procedures other than those that have been bound as formal parameters.
For example, suppose we wish to compute the function
\[
  f(x,y) = x (1 + xy)^2 + y (1 - y) + (1 + xy) (1 - y) \,,
\]
which we could also express as
\begin{align*}
	a       &= 1 + x y  \,, \\
	b       &= 1 - y    \,, \\
	f(x, y) &= x a^2 + y b + a b \,.
\end{align*}
In writing a procedure to compute \( f \), we would like to include as local variables not only \( x \) and \( y \) but also the names of intermediate quantities like \( a \) and \( b \).
One way to accomplish this is to use an auxiliary procedure to bind the local variables:
\begin{scheme}
  (define (f x y)
    (define (f-helper a b)
      (+ (* x (square a))
         (* y b)
         (* a b)))
    (f-helper (+ 1 (* x y))
              (- 1 y)))
\end{scheme}

Of course, we could use a \code{lambda} expression to specify an anonymous procedure for binding our local variables.
The body of \code{f} then becomes a single call to that procedure:
\begin{scheme}
  (define (f x y)
    ((lambda (a b)
       (+ (* x (square a))
          (* y b)
          (* a b)))
     (+ 1 (* x y))
     (- 1 y)))
\end{scheme}
This construct is so useful that there is a special form called \code{let} to make its use more convenient.
Using \code{let}, the \code{f} procedure could be written as
\begin{scheme}
  (define (f x y)
    (let ((a (+ 1 (* x y)))
          (b (- 1 y)))
      (+ (* x (square a))
         (* y b)
         (* a b))))
\end{scheme}
The general form of a \code{let} expression is
\begin{scheme}
  (let ((⟨~\var{var}\ind{1}~⟩ ⟨~\var{exp}\ind{1}~⟩)
        (⟨~\var{var}\ind{2}~⟩ ⟨~\var{exp}\ind{2}~⟩)
        ⋮
        (⟨~\var{var}\ind{n}~⟩ ⟨~\var{exp}\ind{n}~⟩)
     ⟨~\var{body}~⟩)
\end{scheme}
which can be thought of as saying
\begin{scheme}
  ~let ⟨\var{var}\ind{1}⟩ have the value ⟨\var{exp}\ind{1}⟩ and~
      ~⟨\var{var}\ind{2}⟩ have the value ⟨\var{exp}\ind{2}⟩ and~
       ⋮
      ~⟨\var{var}\ind{n}⟩ have the value ⟨\var{exp}\ind{n}⟩ and~
  ~in~  ~⟨\var{body}⟩~
\end{scheme}

The first part of the \code{let} expression is a list of name-expression pairs.
When the \code{let} is evaluated, each name is associated with the value of the corresponding expression.
The body of the \code{let} is evaluated with these names bound as local variables.
The way this happens is that the \code{let} expression is interpreted as an alternate syntax for
\begin{scheme}
  ((lambda (⟨~\var{var}\ind{1}~⟩ … ⟨~\var{var}\ind{n}~⟩)
      ⟨~\var{body}~⟩)
   ⟨~\var{exp}\ind{1}⟩~
   ⋮
   ⟨~\var{exp}\ind{n}⟩~)
\end{scheme}
No new mechanism is required in the interpreter in order to provide local variables.
A \code{let} expression is simply syntactic sugar for the underlying \code{lambda} application.

We can see from this equivalence that the scope of a variable specified by a
\code{let} expression is the body of the \code{let}.  This implies that:
\begin{itemize}

	\item
		\code{let} allows one to bind variables as locally as possible to where they are to be used.
		For example, if the value of \code{x} is \( 5 \), the value of the expression
		\begin{scheme}
		  (+ (let ((x 3))
		       (+ x (* x 10)))
		     x)
		\end{scheme}
		is \( 38 \).
		Here, the \code{x} in the body of the \code{let} is \( 3 \), so the value of the \code{let} expression is \( 33 \).
		On the other hand, the \code{x} that is the second argument to the outermost \code{+} is still \( 5 \).

	\item
		The variables’ values are computed outside the \code{let}.
		This matters when the expressions that provide the values for the local variables depend upon variables having the same names as the local variables themselves.
		For example, if the value of \code{x} is \( 2 \), the expression
		\begin{scheme}
		  (let ((x 3)
		        (y (+ x 2)))
		    (* x y))
		\end{scheme}
		will have the value \( 12 \) because, inside the body of the \code{let}, \code{x} will be \( 3 \) and \code{y} will be \( 4 \) (which is the outer \code{x} plus \( 2 \)).

\end{itemize}

Sometimes we can use internal definitions to get the same effect as with \code{let}.
For example, we could have defined the procedure \code{f} above as
\begin{scheme}
  (define (f x y)
    (define a (+ 1 (* x y)))
    (define b (- 1 y))
    (+ (* x (square a))
       (* y b)
       (* a b)))
\end{scheme}
We prefer, however, to use \code{let} in situations like this and to use internal \code{define} only for internal procedures.%
\footnote{
	Understanding internal definitions well enough to be sure a program means what we intend it to mean requires a more elaborate model of the evaluation process than we have presented in this chapter.
	The subtleties do not arise with internal definitions of procedures, however.
	We will return to this issue in \link{Section 4.1.6}, after we learn more about evaluation.
}



\begin{exercise}
\label{Exercise 1.34}
	Suppose we define the procedure
	\begin{scheme}
	  (define (f g) (g 2))
	\end{scheme}
	Then we have
	\begin{scheme}
	  (f square)
	  ~\outprint{4}~

	  (f (lambda (z) (* z (+ z 1))))
	  ~\outprint{6}~
	\end{scheme}
	What happens if we (perversely) ask the interpreter to evaluate the combination \code{(f f)}?
	Explain.
\end{exercise}



\subsection{Procedures as General Methods}
\label{Section 1.3.3}

We introduced compound procedures in \link{Section 1.1.4} as a mechanism for abstracting patterns of numerical operations so as to make them independent of the particular numbers involved.
With higher-order procedures, such as the \code{integral} procedure of \link{Section 1.3.1}, we began to see a more powerful kind of abstraction:
procedures used to express general methods of computation, independent of the particular functions involved.
In this section we discuss two more elaborate examples---general methods for finding zeros and fixed points of functions---and show how these methods can be expressed directly as procedures.



\subsubsection*{Finding roots of equations by the half-interval method}

The \newterm{half-interval method} is a simple but powerful technique for finding roots of an equation \( f(x) = 0 \), where \( f \) is a continuous function.
The idea is that, if we are given points \( a \) and \( b \) such that \( f(a) < 0 < f(b) \), then \( f \) must have at least one zero between \( a \) and \( b \).
To locate a zero, let \( x \) be the average of \( a \) and \( b \), and compute \( f(x) \).
If \( f(x) > 0 \), then \( f \) must have a zero between \( a \) and \( x \).
If \( f(x) < 0 \), then \( f \) must have a zero between \( x \) and \( b \).
Continuing in this way, we can identify smaller and smaller intervals on which \( f \) must have a zero.
When we reach a point where the interval is small enough, the process stops.
Since the interval of uncertainty is reduced by half at each step of the process, the number of steps required grows as \( Θ(\log(L / T)) \), where \( L \) is the length of the original interval and \( T \) is the error tolerance (that is, the size of the interval we will consider “small enough”).
Here is a procedure that implements this strategy:
\begin{scheme}
  (define (search f neg-point pos-point)
    (let ((midpoint (average neg-point pos-point)))
      (if (close-enough? neg-point pos-point)
          midpoint
          (let ((test-value (f midpoint)))
            (cond ((positive? test-value)
                   (search f neg-point midpoint))
                  ((negative? test-value)
                   (search f midpoint pos-point))
                  (else midpoint))))))
\end{scheme}

We assume that we are initially given the function \( f \) together with points at which its values are negative and positive.
We first compute the midpoint of the two given points.
Next we check to see if the given interval is small enough, and if so we simply return the midpoint as our answer.
Otherwise, we compute as a test value the value of \( f \) at the midpoint.
If the test value is positive, then we continue the process with a new interval running from the original negative point to the midpoint.
If the test value is negative, we continue with the interval from the midpoint to the positive point.
Finally, there is the possibility that the test value is \( 0 \), in which case the midpoint is itself the root we are searching for.

To test whether the endpoints are “close enough” we can use a procedure similar to the one used in \link{Section 1.1.7} for computing square roots:%
\footnote{
	We have used \( 0.001 \) as a representative “small” number to indicate a tolerance for the acceptable error in a calculation.
	The appropriate tolerance for a real calculation depends upon the problem to be solved and the limitations of the computer and the algorithm.
	This is often a very subtle consideration, requiring help from a numerical analyst or some other kind of magician.
}
\begin{scheme}
  (define (close-enough? x y) (< (abs (- x y)) 0.001))
\end{scheme}

\code{search} is awkward to use directly, because we can accidentally give it points at which \( f \)’s values do not have the required sign, in which case we get a wrong answer.
Instead we will use \code{search} via the following procedure, which checks to see which of the endpoints has a negative function value and which has a positive value, and calls the \code{search} procedure accordingly.
If the function has the same sign on the two given points, the half-interval method cannot be used, in which case the procedure signals an error.%
\footnote{
	This can be accomplished using \code{error}, which takes as arguments a number of items that are printed as error messages.
}
\begin{scheme}
  (define (half-interval-method f a b)
    (let ((a-value (f a))
          (b-value (f b)))
      (cond ((and (negative? a-value) (positive? b-value))
             (search f a b))
            ((and (negative? b-value) (positive? a-value))
             (search f b a))
            (else
             (error "Values are not of opposite sign" a b)))))
\end{scheme}

The following example uses the half-interval method to approximate \( π \) as
the root between \( 2 \) and \( 4 \) of \( \sin x = 0 \):
\begin{scheme}
  (half-interval-method sin 2.0 4.0)
  ~\outprint{3.14111328125}~
\end{scheme}

Here is another example, using the half-interval method to search for a root of
the equation \( x^3 - 2x - 3 = 0 \) between \( 1 \) and \( 2 \):
\begin{scheme}
  (half-interval-method (lambda (x) (- (* x x x) (* 2 x) 3))
                        1.0
                        2.0)
  ~\outprint{1.89306640625}~
\end{scheme}



\subsubsection*{Finding fixed points of functions}

A number \( x \) is called a \newterm{fixed point} of a function \( f \) if \( x \) satisfies the equation \( f(x) = x \).
For some functions \( f \) we can locate a fixed point by beginning with an initial guess and applying \( f \) repeatedly,
$$
	f(x)       \,, \quad
	f(f(x))    \,, \quad
	f(f(f(x))) \,, \quad
	\dotsc
$$
until the value does not change very much.
Using this idea, we can devise a procedure \code{fixed-point} that takes as inputs a function and an initial guess and produces an approximation to a fixed point of the function.
We apply the function repeatedly until we find two successive values whose difference is less than some prescribed tolerance:
\begin{scheme}
  (define tolerance 0.00001)
  (define (fixed-point f first-guess)
    (define (close-enough? v1 v2)
      (< (abs (- v1 v2))
         tolerance))
    (define (try guess)
      (let ((next (f guess)))
        (if (close-enough? guess next)
            next
            (try next))))
    (try first-guess))
\end{scheme}
For example, we can use this method to approximate the fixed point of the cosine function, starting with \( 1 \) as an initial approximation:%
\footnote{
	Try this during a boring lecture:
	Set your calculator to radians mode and then repeatedly press the \code{cos} button until you obtain the fixed point.
}
\begin{scheme}
  (fixed-point cos 1.0)
  ~\outprint{.7390822985224023}~
\end{scheme}
Similarly, we can find a solution to the equation \( y = \sin y + \cos y \):
\begin{scheme}
  (fixed-point (lambda (y) (+ (sin y) (cos y)))
               1.0)
  ~\outprint{1.2587315962971173}~
\end{scheme}

The fixed-point process is reminiscent of the process we used for finding square roots in \link{Section 1.1.7}.
Both are based on the idea of repeatedly improving a guess until the result satisfies some criterion.
In fact, we can readily formulate the square-root computation as a fixed-point search.
Computing the square root of some number \( x \) requires finding a \( y \) such that \( y^2 = x \).
Putting this equation into the equivalent form \( y = x / y \), we recognize that we are looking for a fixed point of the function%
\footnote{
	\( \mapsto \) (pronounced “maps to”) is the mathematician’s way of writing \code{lambda}.
	\( y \mapsto x / y \) means \code{(lambda (y) (/ x y))}, that is, the function whose value at \( y \) is \( x / y \).
}
\( y \mapsto x / y \), and we can therefore try to compute square roots as
\begin{scheme}
  (define (sqrt x)
    (fixed-point (lambda (y) (/ x y))
                 1.0))
\end{scheme}

Unfortunately, this fixed-point search does not converge.
Consider an initial guess \( y_1 \).
The next guess is \( y_2 = x / y_1 \) and the next guess is \( y_3 = x / y_2 = x / (x / y_1) = y_1 \).
This results in an infinite loop in which the two guesses \( y_1 \) and \( y_2 \) repeat over and over, oscillating about the answer.

One way to control such oscillations is to prevent the guesses from changing so much.
Since the answer is always between our guess \( y \) and \( x / y \), we can make a new guess that is not as far from \( y \) as \( x / y \) by averaging \( y \) with \( x / y \), so that the next guess after \( y \) is \( \frac{1}{2} (y + x / y) \) instead of \( x / y \).
The process of making such a sequence of guesses is simply the process of looking for a fixed point of \( y \mapsto \frac{1}{2} (y + x / y) \):
\begin{scheme}
  (define (sqrt x)
    (fixed-point (lambda (y) (average y (/ x y)))
                 1.0))
\end{scheme}
(Note that \( y = \frac{1}{2} (y + x / y) \) is a simple transformation of the
equation \( y = x / y; \) to derive it, add \( y \) to both sides of the
equation and divide by \( 2 \).)

With this modification, the square-root procedure works.
In fact, if we unravel the definitions, we can see that the sequence of approximations to the square root generated here is precisely the same as the one generated by our original square-root procedure of \link{Section 1.1.7}.
This approach of averaging successive approximations to a solution, a technique that we call \newterm{average damping}, often aids the convergence of fixed-point searches.



\begin{exercise}
	\label{Exercise 1.35}
	Show that the golden ratio \( ϕ \) (\link{Section 1.2.2}) is a fixed point of the transformation \( x \mapsto 1 + 1 / x \), and use this fact to compute \( ϕ \) by means of the \code{fixed-point} procedure.
\end{exercise}



\begin{exercise}
	\label{Exercise 1.36}
	Modify \code{fixed-point} so that it prints the sequence of approximations it generates, using the \code{newline} and \code{display} primitives shown in \link{Exercise 1.22}.
	Then find a solution to \( x^x = 1000 \) by finding a fixed point of \( x \mapsto \log(1000) / \log(x) \).
	(Use Scheme’s primitive \code{log} procedure, which computes natural logarithms.)
	Compare the number of steps this takes with and without average damping.
	(Note that you cannot start \code{fixed-point} with a guess of \( 1 \), as this would cause division by \( \log(1) = 0 \).)
\end{exercise}



\begin{exercise}
	\label{Exercise 1.37}
	\begin{enumerate}[label = \alph*., leftmargin = *]

		\item
			An infinite \newterm{continued fraction} is an expression of the form
			\[
				f = \cfrac{N_1}{D_1 + \cfrac{N_2}{D_2 + \cfrac{N_3}{D_3 + \dotsb}}} \,.
			\]
			As an example, one can show that the infinite continued fraction expansion with the \( N_i \) and the \( D_i \) all equal to 1 produces \( 1 / ϕ \), where \( ϕ \) is the golden ratio (described in \link{Section 1.2.2}).
			One way to approximate an infinite continued fraction is to truncate the expansion after a given number of terms.
			Such a truncation---a so-called \newterm{\( k \)-term finite continued fraction}---has the form
			\[
				\cfrac{N_1}{D_1 + \cfrac{N_2}{\ddots + \cfrac{N_k}{D_k}}} \,.
			\]
			Suppose that \code{n} and \code{d} are procedures of one argument (the term index \( i \)) that return the \( N_i \) and \( D_i \) of the terms of the continued fraction.
			Define a procedure \code{cont-frac} such that evaluating \code{(cont-frac n d k)} computes the value of the \( k \)-term finite continued fraction.
			Check your procedure by approximating \( 1 / ϕ \) using
			\begin{scheme}
			  (cont-frac (lambda (i) 1.0)
			             (lambda (i) 1.0)
			             k)
			\end{scheme}
			for successive values of \code{k}.
			How large must you make \code{k} in order to get an approximation that is accurate to \( 4 \) decimal places?

		\item
			If your \code{cont-frac} procedure generates a recursive process, write one that generates an iterative process.
			If it generates an iterative process, write one that generates a recursive process.

	\end{enumerate}
\end{exercise}



\begin{exercise}
	\label{Exercise 1.38}
	In 1737, the Swiss mathematician Leonhard Euler published a memoir \textit{De Fractionibus Continuis}, which included a continued fraction expansion for \( e - 2 \), where \( e \) is the base of the natural logarithms.
	In this fraction, the \( N_i \) are all \( 1 \), and the \( D_i \) are successively \( 1, 2, 1, 1, 4, 1, 1, 6, 1, 1, 8, \dotsc \)
	Write a program that uses your \code{cont-frac} procedure from \link{Exercise 1.37} to approximate \( e \), based on Euler’s expansion.
\end{exercise}



\begin{exercise}
	\label{Exercise 1.39}
	A continued fraction  representation of the tangent function was published in 1770 by the German mathematician J.H. Lambert:
	\[
		\tan x = \cfrac{x}{1 - \cfrac{x^2}{3 - \cfrac{x^2}{5 - \ddots}}}\,,
	\]
	where \( x \) is in radians.
	Define a procedure \code{(tan-cf x k)} that computes an approximation to the tangent function based on Lambert’s formula.
	\code{k} specifies the number of terms to compute, as in \link{Exercise 1.37}.
\end{exercise}



\subsection{Procedures as Returned Values}
\label{Section 1.3.4}

The above examples demonstrate how the ability to pass procedures as arguments significantly enhances the expressive power of our programming language.
We can achieve even more expressive power by creating procedures whose returned values are themselves procedures.

We can illustrate this idea by looking again at the fixed-point example described at the end of \link{Section 1.3.3}.
We formulated a new version of the square-root procedure as a fixed-point search, starting with the observation that \( \sqrt{x} \) is a fixed-point of the function \( y \mapsto x / y \).
Then we used average damping to make the approximations converge.
Average damping is a useful general technique in itself.
Namely, given a function \( f \), we consider the function whose value at \( x \) is equal to the average of \( x \) and \( f(x) \).

We can express the idea of average damping by means of the following procedure:
\begin{scheme}
  (define (average-damp f)
    (lambda (x) (average x (f x))))
\end{scheme}
\code{average-damp} is a procedure that takes as its argument a procedure \code{f} and returns as its value a procedure (produced by the \code{lambda}) that, when applied to a number \code{x}, produces the average of \code{x} and \code{(f x)}.
For example, applying \code{average-damp} to the \code{square} procedure produces a procedure whose value at some number \( x \) is the average of \( x \) and \( x^2 \).
Applying this resulting procedure to \( 10 \) returns the average of \( 10 \) and \( 100 \), or \( 55 \):%
\footnote{
	Observe that this is a combination whose operator is itself a combination.
	\link{Exercise 1.4} already demonstrated the ability to form such combinations, but that was only a toy example.
	Here we begin to see the real need for such combinations---when applying a procedure that is obtained as the value returned by a higher-order procedure.
}
\begin{scheme}
  ((average-damp square) 10)
  ~\outprint{55}~
\end{scheme}
Using \code{average-damp}, we can reformulate the square-root procedure as follows:
\begin{scheme}
  (define (sqrt x)
    (fixed-point (average-damp (lambda (y) (/ x y)))
                 1.0))
\end{scheme}
Notice how this formulation makes explicit the three ideas in the method:
fixed-point search, average damping, and the function \( y \mapsto x / y \).
It is instructive to compare this formulation of the square-root method with the original version given in \link{Section 1.1.7}.
Bear in mind that these procedures express the same process, and notice how much clearer the idea becomes when we express the process in terms of these abstractions.
In general, there are many ways to formulate a process as a procedure.
Experienced programmers know how to choose procedural formulations that are particularly perspicuous, and where useful elements of the process are exposed as separate entities that can be reused in other applications.
As a simple example of reuse, notice that the cube root of \( x \) is a fixed point of the function \( y \mapsto x / y^2 \), so we can immediately generalize our square-root procedure to one that extracts cube roots:%
\footnote{
	See \link{Exercise 1.45} for a further generalization.
}
\begin{scheme}
  (define (cube-root x)
    (fixed-point (average-damp (lambda (y) (/ x (square y))))
                 1.0))
\end{scheme}



\subsubsection*{Newton’s method}

When we first introduced the square-root procedure, in \link{Section 1.1.7}, we mentioned that this was a special case of \newterm{Newton’s method}.
If \( x \mapsto g(x) \) is a differentiable function, then a solution of the equation \( g(x) = 0 \) is a fixed point of the function \( x \mapsto f(x) \), where
\[
	f(x) = x - \frac{g(x)}{Dg(x)}
\]

\noindent
and \( Dg(x) \) is the derivative of \( g \) evaluated at \( x \).
Newton’s method is the use of the fixed-point method we saw above to approximate a solution of the equation by finding a fixed point of the function \( f \).%
\footnote{
	Elementary calculus books usually describe Newton’s method in terms of the sequence of approximations \( x_{n+1} = x_n - g(x_n) / Dg(x_n) \).
	Having language for talking about processes and using the idea of fixed points simplifies the description of the method.
}
For many functions \( g \) and for sufficiently good initial guesses for \( x \),
Newton’s method converges very rapidly to a solution of \( g(x) = 0 \).%
\footnote{
	Newton’s method does not always converge to an answer, but it can be shown that in favorable cases each iteration doubles the number-of-digits accuracy of the approximation to the solution.
	In such cases, Newton’s method will converge much more rapidly than the half-interval method.
}

In order to implement Newton’s method as a procedure, we must first express the idea of derivative.
Note that “derivative,” like average damping, is something that transforms a function into another function.
For instance, the derivative of the function \( x \mapsto x^3 \) is the function \( x \mapsto 3x^2\!.
\) In general, if \( g \) is a function and \( dx \) is a small number, then the derivative \( Dg \) of \( g \) is the function whose value at any number \( x \) is given (in the limit of small \( dx \)) by
\[
	Dg(x) = \frac{g(x + dx) - g(x)}{dx}\,.
\]
Thus, we can express the idea of derivative (taking \( dx \) to be, say,
\( 0.00001 \)) as the procedure
\begin{scheme}
  (define (deriv g)
    (lambda (x) (/ (- (g (+ x dx)) (g x)) dx)))
\end{scheme}
along with the definition
\begin{scheme}
  (define dx 0.00001)
\end{scheme}

Like \code{average-damp}, \code{deriv} is a procedure that takes a procedure as argument and returns a procedure as value.
For example, to approximate the derivative of \( x \mapsto x^3 \) at \( 5 \) (whose exact value is \( 75 \)) we can evaluate
\begin{scheme}
  (define (cube x) (* x x x))
  ((deriv cube) 5)
  ~\outprint{75.00014999664018}~
\end{scheme}

With the aid of \code{deriv}, we can express Newton’s method as a fixed-point
process:
\begin{scheme}
  (define (newton-transform g)
    (lambda (x) (- x (/ (g x) ((deriv g) x)))))

  (define (newtons-method g guess)
    (fixed-point (newton-transform g) guess))
\end{scheme}
The \code{newton-transform} procedure expresses the formula at the beginning of this section, and \code{newtons-method} is readily defined in terms of this.
It takes as arguments a procedure that computes the function for which we want to find a zero, together with an initial guess.
For instance, to find the square root of \( x \), we can use Newton’s method to find a zero of the function \( y \mapsto y^2 - x \) starting with an initial guess of \( 1 \).%
\footnote{
	For finding square roots, Newton’s method converges rapidly to the correct solution from any starting point.
}
This provides yet another form of the square-root procedure:
\begin{scheme}
  (define (sqrt x)
    (newtons-method
     (lambda (y) (- (square y) x)) 1.0))
\end{scheme}



\subsubsection*{Abstractions and first-class procedures}

We’ve seen two ways to express the square-root computation as an instance of a more general method, once as a fixed-point search and once using Newton’s method.
Since Newton’s method was itself expressed as a fixed-point process, we actually saw two ways to compute square roots as fixed points.
Each method begins with a function and finds a fixed point of some transformation of the function.
We can express this general idea itself as a procedure:
\begin{scheme}
  (define (fixed-point-of-transform g transform guess)
    (fixed-point (transform g) guess))
\end{scheme}
This very general procedure takes as its arguments a procedure \code{g} that computes some function, a procedure that transforms \code{g}, and an initial guess.
The returned result is a fixed point of the transformed function.

Using this abstraction, we can recast the first square-root computation from this section (where we look for a fixed point of the average-damped version of \( y \mapsto x / y \)) as an instance of this general method:
\begin{scheme}
  (define (sqrt x)
    (fixed-point-of-transform
     (lambda (y) (/ x y)) average-damp 1.0))
\end{scheme}
Similarly, we can express the second square-root computation from this section (an instance of Newton’s method that finds a fixed point of the Newton transform of \( y \mapsto y^2 - x \)) as
\begin{scheme}
  (define (sqrt x)
    (fixed-point-of-transform
     (lambda (y) (- (square y) x)) newton-transform 1.0))
\end{scheme}

We began \link{Section 1.3} with the observation that compound procedures are a crucial abstraction mechanism, because they permit us to express general methods of computing as explicit elements in our programming language.
Now we’ve seen how higher-order procedures permit us to manipulate these general methods to create further abstractions.

As programmers, we should be alert to opportunities to identify the underlying abstractions in our programs and to build upon them and generalize them to create more powerful abstractions.
This is not to say that one should always write programs in the most abstract way possible;
expert programmers know how to choose the level of abstraction appropriate to their task.
But it is important to be able to think in terms of these abstractions, so that we can be ready to apply them in new contexts.
The significance of higher-order procedures is that they enable us to represent these abstractions explicitly as elements in our programming language, so that they can be handled just like other computational elements.

In general, programming languages impose restrictions on the ways in which computational elements can be manipulated.
Elements with the fewest restrictions are said to have \newterm{first-class} status.
Some of the “rights and privileges” of first-class elements are:%
\footnote{
	The notion of first-class status of programming-language elements is due to the British computer scientist Christopher Strachey (1916--1975).
}
\begin{itemize}

	\item
		They may be named by variables.

	\item
		They may be passed as arguments to procedures.

	\item
		They may be returned as the results of procedures.

	\item
		They may be included in data structures.%
		\footnote{
			We’ll see examples of this after we introduce data structures in \link{Chapter 2}.
		}

\end{itemize}
Lisp, unlike other common programming languages, awards procedures full first-class status.
This poses challenges for efficient implementation, but the resulting gain in expressive power is enormous.%
\footnote{
	The major implementation cost of first-class procedures is that allowing procedures to be returned as values requires reserving storage for a procedure’s free variables even while the procedure is not executing.
	In the Scheme implementation we will study in \link{Section 4.1}, these variables are stored in the procedure’s environment.
}



\begin{exercise}
	\label{Exercise 1.40}
	Define a procedure \code{cubic}  that can be used together with the \code{newtons-method} procedure in expressions of the form
	\begin{scheme}
	  (newtons-method (cubic a b c) 1)
	\end{scheme}
	to approximate zeros of the cubic \( x^3 + ax^2 + bx + c \).
\end{exercise}



\begin{exercise}
	\label{Exercise 1.41}
	Define a procedure \code{double} that takes a procedure of one argument as argument and returns a procedure that applies the original procedure twice.
	For example, if \code{inc} is a procedure that adds \( 1 \) to its argument, then \code{(double inc)} should be a procedure that adds \( 2 \).
	What value is returned by
	\begin{scheme}
	  (((double (double double)) inc) 5)
	\end{scheme}
\end{exercise}



\begin{exercise}
	\label{Exercise 1.42}
	Let \( f \) and \( g \) be two one-argument functions.
	The \newterm{composition} \( f \) after \( g \) is defined to be the function \( x \mapsto f(g(x)) \).
	Define a procedure \code{compose} that implements composition.
	For example, if \code{inc} is a procedure that adds \( 1 \) to its argument,
	\begin{scheme}
	  ((compose square inc) 6)
	  ~\outprint{49}~
	\end{scheme}
\end{exercise}

\begin{exercise}
	\label{Exercise 1.43}
	If \( f \) is a numerical function and \( n \) is a positive integer, then we can form the \( n \)\textsuperscript{th} repeated application of \( f \), which is defined to be the function whose value at \( x \) is \( f(f( \dotsm f(x) \dotsm )) \).
	For example, if \( f \) is the function \( x \mapsto x + 1 \), then the \( n \)\textsuperscript{th} repeated application of \( f \) is the function \( x \mapsto x + n \).
	If \( f \) is the operation of squaring a number, then the \( n \)\textsuperscript{th} repeated application of \( f \) is the function that raises its argument to the (\( 2^n \))\textsuperscript{th} power.
	Write a procedure that takes as inputs a procedure that computes \( f \) and a positive integer \( n \) and returns the procedure that computes the \( n \)\textsuperscript{th} repeated application of \( f \).
	Your procedure should be able to be used as follows:
	\begin{scheme}
	  ((repeated square 2) 5)
	  ~\outprint{625}~
	\end{scheme}
	Hint:
	You may find it convenient to use \code{compose} from \link{Exercise 1.42}.
\end{exercise}



\begin{exercise}
	\label{Exercise 1.44}
	The idea of \newterm{smoothing} a function is an important concept in signal processing.
	If \( f \) is a function and \( dx \) is some small number, then the smoothed version of \( f \) is the function whose value at a point \( x \) is the average of \( f(x - dx) \), \( f(x) \), and \( f(x + dx) \).
	Write a procedure \code{smooth} that takes as input a procedure that computes \( f \) and returns a procedure that computes the smoothed \( f \).
	It is sometimes valuable to repeatedly smooth a function (that is, smooth the smoothed function, and so on) to obtain the \newterm{\textit{n}-fold smoothed function}.
	Show how to generate the \textit{n}-fold smoothed function of any given function using \code{smooth} and \code{repeated} from \link{Exercise 1.43}.
\end{exercise}



\begin{exercise}
	\label{Exercise 1.45}
	We saw in \link{Section 1.3.3} that attempting to compute square roots by naively finding a fixed point of \( y \mapsto x / y \) does not converge, and that this can be fixed by average damping.
	The same method works for finding cube roots as fixed points of the average-damped \( y \mapsto x / y^2 \).
	Unfortunately, the process does not work for fourth roots---a single average damp is not enough to make a fixed-point search for \( y \mapsto x / y^3 \) converge.
	On the other hand, if we average damp twice (i.e., use the average damp of the average damp of \( y \mapsto x / y^3 \)) the fixed-point search does converge.
	Do some experiments to determine how many average damps are required to compute \( n \)\textsuperscript{th} roots as a fixed-point search based upon repeated average damping of \( y \mapsto x / y^{n - 1} \).
	Use this to implement a simple procedure for computing \( n \)\textsuperscript{th} roots using \code{fixed-point}, \code{average-damp}, and the \code{repeated} procedure of \link{Exercise 1.43}.
	Assume that any arithmetic operations you need are available as primitives.
\end{exercise}



\begin{exercise}
	\label{Exercise 1.46}
	Several of the numerical methods described in this chapter are instances of an extremely general computational strategy known as \newterm{iterative improvement}.
	Iterative improvement says that, to compute something, we start with an initial guess for the answer, test if the guess is good enough, and otherwise improve the guess and continue the process using the improved guess as the new guess.
	Write a procedure \code{iterative-improve} that takes two procedures as arguments:
	a method for telling whether a guess is good enough and a method for improving a guess.
	\code{iterative-improve} should return as its value a procedure that takes a guess as argument and keeps improving the guess until it is good enough.
	Rewrite the \code{sqrt} procedure of \link{Section 1.1.7} and the \code{fixed-point} procedure of \link{Section 1.3.3} in terms of \code{iterative-improve}.
\end{exercise}
