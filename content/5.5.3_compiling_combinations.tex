\subsection{Compiling Combinations}
\label{Section 5.5.3}

The essence of the compilation process is the compilation of procedure applications.
The code for a combination compiled with a given target and linkage has the form
\begin{scheme}
  ⟨~\emph{compilation of operator, target \code{proc}, linkage \code{next}}~⟩
  ⟨~\emph{evaluate operands and construct argument list in \code{argl}}~⟩
  ⟨~\emph{compilation of procedure call with given target and linkage}~⟩
\end{scheme}
The registers \code{env}, \code{proc}, and \code{argl} may have to be saved and restored during evaluation of the operator and operands.
Note that this is the only place in the compiler where a target other than \code{val} is specified.

The required code is generated by \code{compile-application}.
This recursively compiles the operator, to produce code that puts the procedure to be applied into \code{proc}, and compiles the operands, to produce code that evaluates the individual operands of the application.
The instruction sequences for the operands are combined (by \code{construct-arglist}) with code that constructs the list of arguments in \code{argl}, and the resulting argument-list code is combined with the procedure code and the code that performs the procedure call (produced by \code{compile-procedure-call}).
In appending the code sequences, the \code{env} register must be preserved around the evaluation of the operator (since evaluating the operator might modify \code{env}, which will be needed to evaluate the operands), and the \code{proc} register must be preserved around the construction of the argument list (since evaluating the operands might modify \code{proc}, which will be needed for the actual procedure application).
\code{continue} must also be preserved throughout, since it is needed for the linkage in the procedure call.

\begin{scheme}
  (define (compile-application exp target linkage)
    (let ((proc-code (compile (operator exp) 'proc 'next))
          (operand-codes
           (map (lambda
                  (operand) (compile operand 'val 'next))
                (operands exp))))
      (preserving '(env continue)
       proc-code
       (preserving '(proc continue)
        (construct-arglist operand-codes)
        (compile-procedure-call target linkage)))))
\end{scheme}

The code to construct the argument list will evaluate each operand into \code{val} and then \code{cons} that value onto the argument list being accumulated in \code{argl}.
Since we \code{cons} the arguments onto \code{argl} in sequence, we must start with the last argument and end with the first, so that the arguments will appear in order from first to last in the resulting list.
Rather than waste an instruction by initializing \code{argl} to the empty list to set up for this sequence of evaluations, we make the first code sequence construct the initial \code{argl}.
The general form of the argument-list construction is thus as follows:

\begin{scheme}
  ⟨~\emph{compilation of last operand, targeted to \code{val}}~⟩
  (assign argl (op list) (reg val))
  ⟨~\emph{compilation of next operand, targeted to \code{val}}~⟩
  (assign argl (op cons) (reg val) (reg argl))
  …
  ⟨~\emph{compilation of first operand, targeted to \code{val}}~⟩
  (assign argl (op cons) (reg val) (reg argl))
\end{scheme}
\code{argl} must be preserved around each operand evaluation except the first (so that arguments accumulated so far won’t be lost), and \code{env} must be preserved around each operand evaluation except the last (for use by subsequent operand evaluations).

Compiling this argument code is a bit tricky, because of the special treatment of the first operand to be evaluated and the need to preserve \code{argl} and \code{env} in different places.
The \code{construct-arglist} procedure takes as arguments the code that evaluates the individual operands.
If there are no operands at all, it simply emits the instruction
\begin{scheme}
  (assign argl (const ()))
\end{scheme}
Otherwise, \code{construct-arglist} creates code that initializes \code{argl} with the last argument, and appends code that evaluates the rest of the arguments and adjoins them to \code{argl} in succession.
In order to process the arguments from last to first, we must reverse the list of operand code sequences from the order supplied by \code{compile-application}.

\begin{scheme}
  (define (construct-arglist operand-codes)
    (let ((operand-codes (reverse operand-codes)))
      (if (null? operand-codes)
          (make-instruction-sequence '() '(argl)
           '((assign argl (const ()))))
          (let ((code-to-get-last-arg
                 (append-instruction-sequences
                  (car operand-codes)
                  (make-instruction-sequence '(val) '(argl)
                   '((assign argl (op list) (reg val)))))))
            (if (null? (cdr operand-codes))
                code-to-get-last-arg
                (preserving '(env)
                 code-to-get-last-arg
                 (code-to-get-rest-args
                  (cdr operand-codes))))))))

  (define (code-to-get-rest-args operand-codes)
    (let ((code-for-next-arg
           (preserving '(argl)
            (car operand-codes)
            (make-instruction-sequence '(val argl) '(argl)
             '((assign argl
                (op cons) (reg val) (reg argl)))))))
      (if (null? (cdr operand-codes))
          code-for-next-arg
          (preserving '(env)
           code-for-next-arg
           (code-to-get-rest-args (cdr operand-codes))))))
\end{scheme}



\subsubsection*{Applying procedures}

After evaluating the elements of a combination, the compiled code must apply the procedure in \code{proc} to the arguments in \code{argl}.
The code performs essentially the same dispatch as the \code{apply} procedure in the metacircular evaluator of \link{Section 4.1.1} or the \code{apply-dispatch} entry point in the explicit-control evaluator of \link{Section 5.4.1}.
It checks whether the procedure to be applied is a primitive procedure or a compiled procedure.
For a primitive procedure, it uses \code{apply-primitive-procedure};
we will see shortly how it handles compiled procedures.
The procedure-application code has the following form:
\begin{scheme}
   (test (op primitive-procedure?) (reg proc))
   (branch (label primitive-branch))
  compiled-branch
   ⟨~\emph{code to apply compiled procedure with given target and appropriate linkage}~⟩
  primitive-branch
   (assign ⟨~\var{target}~⟩
           (op apply-primitive-procedure)
           (reg proc)
           (reg argl))
   ⟨~\var{linkage}~⟩
  after-call
\end{scheme}

Observe that the compiled branch must skip around the primitive branch.
Therefore, if the linkage for the original procedure call was \code{next}, the compound branch must use a linkage that jumps to a label that is inserted after the primitive branch.
(This is similar to the linkage used for the true branch in \code{compile-if}.)
\begin{scheme}
  (define (compile-procedure-call target linkage)
    (let ((primitive-branch (make-label 'primitive-branch))
          (compiled-branch (make-label 'compiled-branch))
          (after-call (make-label 'after-call)))

      (let ((compiled-linkage
             (if (eq? linkage 'next) after-call linkage)))
        (append-instruction-sequences
         (make-instruction-sequence '(proc) '()
          `((test (op primitive-procedure?) (reg proc))
            (branch (label ,primitive-branch))))
         (parallel-instruction-sequences
          (append-instruction-sequences
           compiled-branch
           (compile-proc-appl target compiled-linkage))
          (append-instruction-sequences
           primitive-branch
           (end-with-linkage linkage
            (make-instruction-sequence '(proc argl)
                                       (list target)
             `((assign ,target
                       (op apply-primitive-procedure)
                       (reg proc)
                       (reg argl)))))))
         after-call))))
\end{scheme}
The primitive and compound branches, like the true and false branches in \code{compile-if}, are appended using \code{parallel-instruction-sequences} rather than the ordinary \code{append-instruction-sequences}, because they will not be executed sequentially.



\subsubsection*{Applying compiled procedures}

The code that handles procedure application is the most subtle part of the compiler, even though the instruction sequences it generates are very short.
A compiled procedure (as constructed by \code{compile-lambda}) has an entry point, which is a label that designates where the code for the procedure starts.
The code at this entry point computes a result in \code{val} and returns by executing the instruction \code{(goto (reg continue))}.
Thus, we might expect the code for a compiled-procedure application (to be generated by \code{compile-proc-appl}) with a given target and linkage to look like this if the linkage is a label
\begin{scheme}
   (assign continue (label proc-return))
   (assign val (op compiled-procedure-entry) (reg proc))
   (goto (reg val))
  proc-return
   (assign ⟨~\var{target}~⟩ (reg val))   ~\textrm{; included if target is not \code{val}}~
   (goto (label ⟨~\var{linkage}~⟩))      ~\textrm{; linkage code}~
\end{scheme}
or like this if the linkage is \code{return}.
\begin{scheme}
 (save continue)
 (assign continue (label proc-return))
 (assign val (op compiled-procedure-entry) (reg proc))
 (goto (reg val))
proc-return
 (assign ⟨~\var{target}~⟩ (reg val))   ~\textrm{; included if target is not \code{val}}~
 (restore continue)
 (goto (reg continue))         ~\textrm{; linkage code}~
\end{scheme}
This code sets up \code{continue} so that the procedure will return to a label \code{proc-return} and jumps to the procedure’s entry point.
The code at \code{proc-return} transfers the procedure’s result from \code{val} to the target register (if necessary) and then jumps to the location specified by the linkage.
(The linkage is always \code{return} or a label, because \code{compile-procedure-call} replaces a \code{next} linkage for the compound-procedure branch by an \code{after-call} label.)

In fact, if the target is not \code{val}, that is exactly the code our compiler will generate.%
\footnote{
	Actually, we signal an error when the target is not \code{val} and the linkage is \code{return}, since the only place we request \code{return} linkages is in compiling procedures, and our convention is that procedures return their values in \code{val}.
}
	Usually, however, the target is \code{val} (the only time the compiler specifies a different register is when targeting the evaluation of an operator to \code{proc}), so the procedure result is put directly into the target register and there is no need to return to a special location that copies it.
	Instead, we simplify the code by setting up \code{continue} so that the procedure will “return” directly to the place specified by the caller’s linkage:
\begin{scheme}
  ⟨~\emph{set up \code{continue} for linkage}~⟩
  (assign val (op compiled-procedure-entry) (reg proc))
  (goto (reg val))
\end{scheme}
If the linkage is a label, we set up \code{continue} so that the procedure will return to that label.
(That is, the \code{(goto (reg continue))} the procedure ends with becomes equivalent to the \code{(goto (label ⟨\var{linkage}⟩))} at \code{proc-return} above.)
\begin{scheme}
  (assign continue (label ⟨~\var{linkage}~⟩))
  (assign val (op compiled-procedure-entry) (reg proc))
  (goto (reg val))
\end{scheme}
If the linkage is \code{return}, we don’t need to set up \code{continue} at all:
It already holds the desired location.
(That is, the \code{(goto (reg continue))} the procedure ends with goes directly to the place where the \code{(goto (reg continue))} at \code{proc-return} would have gone.)
\begin{scheme}
  (assign val (op compiled-procedure-entry) (reg proc))
  (goto (reg val))
\end{scheme}
With this implementation of the \code{return} linkage, the compiler generates tail-recursive code.
Calling a procedure as the final step in a procedure body does a direct transfer, without saving any information on the stack.

Suppose instead that we had handled the case of a procedure call with a linkage of \code{return} and a target of \code{val} as shown above for a non-\code{val} target.
This would destroy tail recursion.
Our system would still give the same value for any expression.
But each time we called a procedure, we would save \code{continue} and return after the call to undo the (useless) save.
These extra saves would accumulate during a nest of procedure calls.%
\footnote{
	Making a compiler generate tail-recursive code might seem like a straightforward idea.
	But most compilers for common languages, including C and Pascal, do not do this, and therefore these languages cannot represent iterative processes in terms of procedure call alone.
	The difficulty with tail recursion in these languages is that their implementations use the stack to store procedure arguments and local variables as well as return addresses.
	The Scheme implementations described in this book store arguments and variables in memory to be garbage-collected.
	The reason for using the stack for variables and arguments is that it avoids the need for garbage collection in languages that would not otherwise require it, and is generally believed to be more efficient.
	Sophisticated Lisp compilers can, in fact, use the stack for arguments without destroying tail recursion.
	(See \link{Hanson 1990} for a description.)
	There is also some debate about whether stack allocation is actually more efficient than garbage collection in the first place, but the details seem to hinge on fine points of computer architecture.
	(See \autocite{appel_garbage_faster_than_stack} and \link{Miller and Rozas 1994} for opposing views on this issue.)
}

\code{compile-proc-appl} generates the above procedure-application code by considering four cases, depending on whether the target for the call is \code{val} and whether the linkage is \code{return}.
Observe that the instruction sequences are declared to modify all the registers, since executing the procedure body can change the registers in arbitrary ways.%
\footnote{
	The variable \code{all-regs} is bound to the list of names of all the registers:
	\begin{smallscheme}
	  (define all-regs '(env proc val argl continue))
	\end{smallscheme}
}
Also note that the code sequence for the case with target \code{val} and linkage \code{return} is declared to need \code{continue}:
Even though \code{continue} is not explicitly used in the two-instruction sequence, we must be sure that \code{continue} will have the correct value when we enter the compiled procedure.

\begin{scheme}
  (define (compile-proc-appl target linkage)
    (cond ((and (eq? target 'val) (not (eq? linkage 'return)))
           (make-instruction-sequence '(proc) all-regs
             `((assign continue (label ,linkage))
               (assign val (op compiled-procedure-entry)
                           (reg proc))
               (goto (reg val)))))
          ((and (not (eq? target 'val))
                (not (eq? linkage 'return)))
           (let ((proc-return (make-label 'proc-return)))
             (make-instruction-sequence '(proc) all-regs
              `((assign continue (label ,proc-return))
                (assign val (op compiled-procedure-entry)
                            (reg proc))
                (goto (reg val))
                ,proc-return
                (assign ,target (reg val))
                (goto (label ,linkage))))))
          ((and (eq? target 'val) (eq? linkage 'return))
           (make-instruction-sequence
            '(proc continue)
            all-regs
            '((assign val (op compiled-procedure-entry)
                          (reg proc))
              (goto (reg val)))))
          ((and (not (eq? target 'val))
                (eq? linkage 'return))
           (error "return linkage, target not val: COMPILE"
                  target))))
\end{scheme}
