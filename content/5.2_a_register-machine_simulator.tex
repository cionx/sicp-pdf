\section{A Register-Machine Simulator}
\label{Section 5.2}

In order to gain a good understanding of the design of register machines, we must test the machines we design to see if they perform as expected.
One way to test a design is to hand-simulate the operation of the controller, as in \link{Exercise 5.5}.
But this is extremely tedious for all but the simplest machines.
In this section we construct a simulator for machines described in the register-machine language.
The simulator is a Scheme program with four interface procedures.
The first uses a description of a register machine to construct a model of the machine (a data structure whose parts correspond to the parts of the machine to be simulated), and the other three allow us to simulate the machine by manipulating the model:

\begin{itemize}

	\item
		\begin{scheme}
		  (make-machine ⟨~\var{\dark register-names}~⟩ ⟨~\var{operations}~⟩ ⟨~\var{controller}~⟩)
		\end{scheme}
		constructs and returns a model of the machine with the given registers, operations, and controller.

	\item
		\begin{scheme}
		  (set-register-contents! ⟨~\var{\dark machine-model}~⟩
		                          ⟨~\var{\dark register-name}~⟩
		                          ⟨~\var{value}~⟩)
		\end{scheme}
		stores a value in a simulated register in the given machine.

	\item
		\begin{scheme}
		  (get-register-contents ⟨~\var{\dark machine-model}~⟩ ⟨~\var{\dark register-name}~⟩)
		\end{scheme}
		returns the contents of a simulated register in the given machine.

	\item
		\begin{scheme}
		  (start ⟨~\var{\dark machine-model}~⟩)
		\end{scheme}
		simulates the execution of the given machine, starting from the beginning of the controller sequence and stopping when it reaches the end of the sequence.

\end{itemize}

As an example of how these procedures are used, we can define \code{gcd-machine} to be a model of the \acronym{GCD} machine of \link{Section 5.1.1} as follows:
\begin{scheme}
  (define gcd-machine
    (make-machine
     '(a b t)
     (list (list 'rem remainder) (list '= =))
     '(test-b (test (op =) (reg b) (const 0))
              (branch (label gcd-done))
              (assign t (op rem) (reg a) (reg b))
              (assign a (reg b))
              (assign b (reg t))
              (goto (label test-b))
              gcd-done)))
\end{scheme}
The first argument to \code{make-machine} is a list of register names.
The next argument is a table (a list of two-element lists) that pairs each operation name with a Scheme procedure that implements the operation (that is, produces the same output value given the same input values).
The last argument specifies the controller as a list of labels and machine instructions, as in \link{Section 5.1}.

To compute \acronym{GCD}s with this machine, we set the input registers, start the machine, and examine the result when the simulation terminates:
\begin{scheme}
  (set-register-contents! gcd-machine 'a 206)
  ~\outprint{done}~

  (set-register-contents! gcd-machine 'b 40)
  ~\outprint{done}~

  (start gcd-machine)
  ~\outprint{done}~

  (get-register-contents gcd-machine 'a)
  ~\outprint{2}~
\end{scheme}
This computation will run much more slowly than a \code{gcd} procedure written in Scheme, because we will simulate low-level machine instructions, such as \code{assign}, by much more complex operations.



\begin{exercise}
	\label{Exercise 5.7}
	Use the simulator to test the machines you designed in \link{Exercise 5.4}.
\end{exercise}



\subsection{The Machine Model}
\label{Section 5.2.1}

The machine model generated by \code{make-machine} is represented as a procedure with local state using the message-passing techniques developed in \link{Chapter 3}.
To build this model, \code{make-machine} begins by calling the procedure \code{make-new-machine} to construct the parts of the machine model that are common to all register machines.
This basic machine model constructed by \code{make-new-machine} is essentially a container for some registers and a stack, together with an execution mechanism that processes the controller instructions one by one.

\code{make-machine} then extends this basic model (by sending it messages) to include the registers, operations, and controller of the particular machine being defined.
First it allocates a register in the new machine for each of the supplied register names and installs the designated operations in the machine.
Then it uses an \newterm{assembler} (described below in \link{Section 5.2.2}) to transform the controller list into instructions for the new machine and installs these as the machine’s instruction sequence.
\code{make-machine} returns as its value the modified machine model.

\begin{scheme}
  (define (make-machine register-names ops controller-text)
    (let ((machine (make-new-machine)))
      (for-each
       (lambda (register-name)
         ((machine 'allocate-register) register-name))
       register-names)
      ((machine 'install-operations) ops)
      ((machine 'install-instruction-sequence)
       (assemble controller-text machine))
      machine))
\end{scheme}



\subsubsection*{Registers}

We will represent a register as a procedure with local state, as in \link{Chapter 3}.
The procedure \code{make-register} creates a register that holds a value that can be accessed or changed:
\begin{scheme}
  (define (make-register name)
    (let ((contents '*unassigned*))
      (define (dispatch message)
        (cond ((eq? message 'get) contents)
              ((eq? message 'set)
               (lambda (value) (set! contents value)))
              (else
               (error "Unknown request: REGISTER" message))))
      dispatch))
\end{scheme}
The following procedures are used to access registers:

\begin{scheme}
  (define (get-contents register) (register 'get))
  (define (set-contents! register value)
    ((register 'set) value))
\end{scheme}



\subsubsection*{The stack}

We can also represent a stack as a procedure with local state.
The procedure \code{make-stack} creates a stack whose local state consists of a list of the items on the stack.
A stack accepts requests to \code{push} an item onto the stack, to \code{pop} the top item off the stack and return it, and to \code{initialize} the stack to empty.

\begin{scheme}
  (define (make-stack)
    (let ((s '()))
      (define (push x) (set! s (cons x s)))
      (define (pop)
        (if (null? s)
            (error "Empty stack: POP")
            (let ((top (car s)))
              (set! s (cdr s))
              top)))
      (define (initialize)
        (set! s '())
        'done)
      (define (dispatch message)
        (cond ((eq? message 'push) push)
              ((eq? message 'pop) (pop))
              ((eq? message 'initialize) (initialize))
              (else (error "Unknown request: STACK" message))))
      dispatch))
\end{scheme}
The following procedures are used to access stacks:
\begin{scheme}
  (define (pop stack) (stack 'pop))

  (define (push stack value) ((stack 'push) value))
\end{scheme}



\subsubsection*{The basic machine}

The \code{make-new-machine} procedure, shown in \link{Figure 5.13}, constructs an object whose local state consists of a stack, an initially empty instruction sequence, a list of operations that initially contains an operation to initialize the stack, and a \newterm{register table} that initially contains two registers, named \code{flag} and \code{pc} (for “program counter”).
The internal procedure \code{allocate-register} adds new entries to the register table, and the internal procedure \code{lookup-register} looks up registers in the table.

The \code{flag} register is used to control branching in the simulated machine.
\code{test} instructions set the contents of \code{flag} to the result of the test (true or false).
\code{branch} instructions decide whether or not to branch by examining the contents of \code{flag}.

The \code{pc} register determines the sequencing of instructions as the machine runs.
This sequencing is implemented by the internal procedure \code{execute}.
In the simulation model, each machine instruction is a data structure that includes a procedure of no arguments, called the \newterm{instruction execution procedure}, such that calling this procedure simulates executing the instruction.
As the simulation runs, \code{pc} points to the place in the instruction sequence beginning with the next instruction to be executed.
\code{execute} gets that instruction, executes it by calling the instruction execution procedure, and repeats this cycle until there are no more instructions to execute (i.e., until \code{pc} points to the end of the instruction sequence).

\begin{figure}
	\centering
	\begin{scheme}
	  (define (make-new-machine)
	    (let ((pc (make-register 'pc))
	          (flag (make-register 'flag))
	          (stack (make-stack))
	          (the-instruction-sequence '()))
	      (let ((the-ops
	             (list (list 'initialize-stack
	                         (lambda () (stack 'initialize)))))
	            (register-table
	             (list (list 'pc pc) (list 'flag flag))))
	        (define (allocate-register name)
	          (if (assoc name register-table)
	              (error "Multiply defined register: " name)
	              (set! register-table
	                    (cons (list name (make-register name))
	                          register-table)))
	          'register-allocated)
	        (define (lookup-register name)
	          (let ((val (assoc name register-table)))
	            (if val
	                (cadr val)
	                (error "Unknown register:" name))))
	        (define (execute)
	          (let ((insts (get-contents pc)))
	            (if (null? insts)
	                'done
	                (begin
	                  ((instruction-execution-proc (car insts)))
	                  (execute)))))
	        (define (dispatch message)
	          (cond ((eq? message 'start)
	                 (set-contents! pc the-instruction-sequence)
	                 (execute))
	                ((eq? message 'install-instruction-sequence)
	                 (lambda (seq)
	                   (set! the-instruction-sequence seq)))
	                ((eq? message 'allocate-register)
	                 allocate-register)
	                ((eq? message 'get-register)
	                 lookup-register)
	                ((eq? message 'install-operations)
	                 (lambda (ops)
	                   (set! the-ops (append the-ops ops))))
	                ((eq? message 'stack) stack)
	                ((eq? message 'operations) the-ops)
	                (else (error "Unknown request: MACHINE"
	                             message))))
	        dispatch)))
	\end{scheme}
	\caption{
		The \code{make-new-machine} procedure, which implements the basic machine model.
	}
	\label{Figure 5.13}
\end{figure}

As part of its operation, each instruction execution procedure modifies \code{pc} to indicate the next instruction to be executed.
\code{branch} and \code{goto} instructions change \code{pc} to point to the new destination.
All other instructions simply advance \code{pc}, making it point to the next instruction in the sequence.
Observe that each call to \code{execute} calls \code{execute} again, but this does not produce an infinite loop because running the instruction execution procedure changes the contents of \code{pc}.

\code{make-new-machine} returns a \code{dispatch} procedure that implements message-passing access to the internal state.
Notice that starting the machine is accomplished by setting \code{pc} to the beginning of the instruction sequence and calling \code{execute}.

For convenience, we provide an alternate procedural interface to a machine’s \code{start} operation, as well as procedures to set and examine register contents, as specified at the beginning of \link{Section 5.2}:
\begin{scheme}
  (define (start machine) (machine 'start))

  (define (get-register-contents machine register-name)
    (get-contents (get-register machine register-name)))

  (define (set-register-contents! machine register-name value)
    (set-contents! (get-register machine register-name)
                   value)
    'done)
\end{scheme}

These procedures (and many procedures in \link{Section 5.2.2} and \link{Section 5.2.3})
use the following to look up the register with a given name in a given machine:
\begin{scheme}
  (define (get-register machine reg-name)
    ((machine 'get-register) reg-name))
\end{scheme}



\subsection{The Assembler}
\label{Section 5.2.2}

The assembler transforms the sequence of controller expressions for a machine into a corresponding list of machine instructions, each with its execution procedure.
Overall, the assembler is much like the evaluators we studied in \link{Chapter 4}---there is an input language (in this case, the register-machine language) and we must perform an appropriate action for each type of expression in the language.

The technique of producing an execution procedure for each instruction is just what we used in \link{Section 4.1.7} to speed up the evaluator by separating analysis from runtime execution.
As we saw in \link{Chapter 4}, much useful analysis of Scheme expressions could be performed without knowing the actual values of variables.
Here, analogously, much useful analysis of register-machine-language expressions can be performed without knowing the actual contents of machine registers.
For example, we can replace references to registers by pointers to the register objects, and we can replace references to labels by pointers to the place in the instruction sequence that the label designates.

Before it can generate the instruction execution procedures, the assembler must know what all the labels refer to, so it begins by scanning the controller text to separate the labels from the instructions.
As it scans the text, it constructs both a list of instructions and a table that associates each label with a pointer into that list.
Then the assembler augments the instruction list by inserting the execution procedure for each instruction.

The \code{assemble} procedure is the main entry to the assembler.
It takes the controller text and the machine model as arguments and returns the instruction sequence to be stored in the model.
\code{assemble} calls \code{extract-labels} to build the initial instruction list and label table from the supplied controller text.
The second argument to \code{extract-labels} is a procedure to be called to process these results:
This procedure uses \code{update-insts!} to generate the instruction execution procedures and insert them into the instruction list, and returns the modified list.

\begin{scheme}
  (define (assemble controller-text machine)
    (extract-labels
     controller-text
     (lambda (insts labels)
       (update-insts! insts labels machine)
       insts)))
\end{scheme}

\code{extract-labels} takes as arguments a list \code{text} (the sequence of controller instruction expressions) and a \code{receive} procedure.
\code{receive} will be called with two values:
(1) a list \code{insts} of instruction data structures, each containing an instruction from \code{text};
and (2) a table called \code{labels}, which associates each label from \code{text} with the position in the list \code{insts} that the label designates.

\begin{scheme}
  (define (extract-labels text receive)
    (if (null? text)
        (receive '() '())
        (extract-labels
         (cdr text)
         (lambda (insts labels)
           (let ((next-inst (car text)))
             (if (symbol? next-inst)
                 (receive insts
                          (cons (make-label-entry next-inst
                                                  insts)
                                labels))
                 (receive (cons (make-instruction next-inst)
                                insts)
                          labels)))))))
\end{scheme}
\code{extract-labels} works by sequentially scanning the elements of the \code{text} and accumulating the \code{insts} and the \code{labels}.
If an element is a symbol (and thus a label) an appropriate entry is added to the \code{labels} table.
Otherwise the element is accumulated onto the \code{insts} list.%
\footnote{
	Using the \code{receive} procedure here is a way to get \code{extract-labels} to effectively return two values---\code{labels} and \code{insts}---without explicitly making a compound data structure to hold them.
	An alternative implementation, which returns an explicit pair of values, is
	\begin{smallscheme}
	  (define (extract-labels text)
	    (if (null? text)
	        (cons '() '())
	        (let ((result (extract-labels (cdr text))))
	          (let ((insts (car result)) (labels (cdr result)))
	            (let ((next-inst (car text)))
	              (if (symbol? next-inst)
	                  (cons insts
	                        (cons (make-label-entry next-inst insts)
	                              labels))
	                  (cons (cons (make-instruction next-inst) insts)
	                        labels)))))))
	\end{smallscheme}
	which would be called by \code{assemble} as follows:
	\begin{smallscheme}
	  (define (assemble controller-text machine)
	    (let ((result (extract-labels controller-text)))
	      (let ((insts (car result)) (labels (cdr result)))
	        (update-insts! insts labels machine)
	        insts)))
	\end{smallscheme}
	You can consider our use of \code{receive} as demonstrating an elegant way to return multiple values, or simply an excuse to show off a programming trick.
	An argument like \code{receive} that is the next procedure to be invoked is called a “continuation.”
	Recall that we also used continuations to implement the backtracking control structure in the \code{amb} evaluator in \link{Section 4.3.3}.
}

\code{update-insts!} modifies the instruction list, which initially contains only the text of the instructions, to include the corresponding execution procedures:
\begin{scheme}
  (define (update-insts! insts labels machine)
    (let ((pc (get-register machine 'pc))
          (flag (get-register machine 'flag))
          (stack (machine 'stack))
          (ops (machine 'operations)))
      (for-each
       (lambda (inst)
         (set-instruction-execution-proc!
          inst
          (make-execution-procedure
           (instruction-text inst)
           labels machine pc flag stack ops)))
       insts)))
\end{scheme}

The machine instruction data structure simply pairs the instruction text with the corresponding execution procedure.
The execution procedure is not yet available when \code{extract-labels} constructs the instruction, and is inserted later by \code{update-insts!}.
\begin{scheme}
  (define (make-instruction text) (cons text '()))

  (define (instruction-text inst) (car inst))

  (define (instruction-execution-proc inst) (cdr inst))

  (define (set-instruction-execution-proc! inst proc)
    (set-cdr! inst proc))
\end{scheme}
The instruction text is not used by our simulator, but it is handy to keep around for debugging (see \link{Exercise 5.16}).

Elements of the label table are pairs:
\begin{scheme}
  (define (make-label-entry label-name insts)
    (cons label-name insts))
\end{scheme}
Entries will be looked up in the table with
\begin{scheme}
  (define (lookup-label labels label-name)
    (let ((val (assoc label-name labels)))
      (if val
          (cdr val)
          (error "Undefined label: ASSEMBLE"
                 label-name))))
\end{scheme}



\begin{exercise}
	\label{Exercise 5.8}
	The following register-machine code
	is ambiguous, because the label \code{here} is defined more than once:
	\begin{scheme}
	  start
	    (goto (label here))
	  here
	    (assign a (const 3))
	    (goto (label there))
	  here
	    (assign a (const 4))
	    (goto (label there))
	  there
	\end{scheme}
	With the simulator as written, what will the contents of register \code{a} be when control reaches \code{there}?
	Modify the \code{extract-labels} procedure so that the assembler will signal an error if the same label name is used to indicate two different locations.
\end{exercise}



\subsection{Generating Execution Procedures for Instructions}
\label{Section 5.2.3}

The assembler calls \code{make-execution-procedure} to generate the execution procedure for an instruction.
Like the \code{analyze} procedure in the evaluator of \link{Section 4.1.7}, this dispatches on the type of instruction to generate the appropriate execution procedure.

\begin{scheme}
  (define (make-execution-procedure
           inst labels machine pc flag stack ops)
    (cond ((eq? (car inst) 'assign)
           (make-assign inst machine labels ops pc))
          ((eq? (car inst) 'test)
           (make-test inst machine labels ops flag pc))
          ((eq? (car inst) 'branch)
           (make-branch inst machine labels flag pc))
          ((eq? (car inst) 'goto)
           (make-goto inst machine labels pc))
          ((eq? (car inst) 'save)
           (make-save inst machine stack pc))
          ((eq? (car inst) 'restore)
           (make-restore inst machine stack pc))
          ((eq? (car inst) 'perform)
           (make-perform inst machine labels ops pc))
          (else
           (error "Unknown instruction type: ASSEMBLE"
                  inst))))
\end{scheme}

For each type of instruction in the register-machine language, there is a generator that builds an appropriate execution procedure.
The details of these procedures determine both the syntax and meaning of the individual instructions in the register-machine language.
We use data abstraction to isolate the detailed syntax of register-machine expressions from the general execution mechanism, as we did for evaluators in \link{Section 4.1.2}, by using syntax procedures to extract and classify the parts of an instruction.



\subsubsection*{\code{assign} instructions}

The \code{make-assign} procedure handles \code{assign} instructions:
\begin{scheme}
  (define (make-assign inst machine labels operations pc)
    (let ((target
           (get-register machine (assign-reg-name inst)))
          (value-exp (assign-value-exp inst)))
      (let ((value-proc
             (if (operation-exp? value-exp)
                 (make-operation-exp
                  value-exp machine labels operations)
                 (make-primitive-exp
                  (car value-exp) machine labels))))
        (lambda ()   ~\textrm{; execution procedure for \code{assign}}~
          (set-contents! target (value-proc))
          (advance-pc pc)))))
\end{scheme}
\code{make-assign} extracts the target register name (the second element of the
instruction) and the value expression (the rest of the list that forms the
instruction) from the \code{assign} instruction using the selectors
\begin{scheme}
  (define (assign-reg-name assign-instruction)
    (cadr assign-instruction))

  (define (assign-value-exp assign-instruction)
    (cddr assign-instruction))
\end{scheme}
The register name is looked up with \code{get-register} to produce the target register object.
The value expression is passed to \code{make-operation-exp} if the value is the result of an operation, and to \code{make-primitive-exp} otherwise.
These procedures (shown below) parse the value expression and produce an execution procedure for the value.
This is a procedure of no arguments, called \code{value-proc}, which will be evaluated during the simulation to produce the actual value to be assigned to the register.
Notice that the work of looking up the register name and parsing the value expression is performed just once, at assembly time, not every time the instruction is simulated.
This saving of work is the reason we use execution procedures, and corresponds directly to the saving in work we obtained by separating program analysis from execution in the evaluator of \link{Section 4.1.7}.

The result returned by \code{make-assign} is the execution procedure for the \code{assign} instruction.
When this procedure is called (by the machine model’s \code{execute} procedure), it sets the contents of the target register to the result obtained by executing \code{value-proc}.
Then it advances the \code{pc} to the next instruction by running the procedure
\begin{scheme}
  (define (advance-pc pc)
    (set-contents! pc (cdr (get-contents pc))))
\end{scheme}
\code{advance-pc} is the normal termination for all instructions except \code{branch} and \code{goto}.



\subsubsection*{\code{test}, \code{branch}, and \code{goto} instructions}

\code{make-test} handles \code{test} instructions in a similar way.
It extracts the expression that specifies the condition to be tested and generates an execution procedure for it.
At simulation time, the procedure for the condition is called, the result is assigned to the \code{flag} register, and the \code{pc} is advanced:
\begin{scheme}
  (define (make-test inst machine labels operations flag pc)
    (let ((condition (test-condition inst)))
      (if (operation-exp? condition)
          (let ((condition-proc
                 (make-operation-exp
                  condition machine labels operations)))
            (lambda ()
              (set-contents! flag (condition-proc))
              (advance-pc pc)))
          (error "Bad TEST instruction: ASSEMBLE" inst))))

  (define (test-condition test-instruction)
    (cdr test-instruction))
\end{scheme}

The execution procedure for a \code{branch} instruction checks the contents of the \code{flag} register and either sets the contents of the \code{pc} to the branch destination (if the branch is taken) or else just advances the \code{pc} (if the branch is not taken).
Notice that the indicated destination in a \code{branch} instruction must be a label, and the \code{make-branch} procedure enforces this.
Notice also that the label is looked up at assembly time, not each time the \code{branch} instruction is simulated.
\begin{scheme}
  (define (make-branch inst machine labels flag pc)
    (let ((dest (branch-dest inst)))
      (if (label-exp? dest)
          (let ((insts
                 (lookup-label
                  labels
                  (label-exp-label dest))))
            (lambda ()
              (if (get-contents flag)
                  (set-contents! pc insts)
                  (advance-pc pc))))
          (error "Bad BRANCH instruction: ASSEMBLE" inst))))

  (define (branch-dest branch-instruction)
    (cadr branch-instruction))
\end{scheme}

A \code{goto} instruction is similar to a branch, except that the destination
may be specified either as a label or as a register, and there is no condition
to check---the \code{pc} is always set to the new destination.
\begin{scheme}
  (define (make-goto inst machine labels pc)
    (let ((dest (goto-dest inst)))
      (cond ((label-exp? dest)
             (let ((insts (lookup-label
                           labels
                           (label-exp-label dest))))
               (lambda () (set-contents! pc insts))))
            ((register-exp? dest)
             (let ((reg (get-register
                         machine
                         (register-exp-reg dest))))
               (lambda ()
                 (set-contents! pc (get-contents reg)))))
            (else (error "Bad GOTO instruction: ASSEMBLE" inst)))))

  (define (goto-dest goto-instruction)
    (cadr goto-instruction))
\end{scheme}



\subsubsection*{Other instructions}

The stack instructions \code{save} and \code{restore} simply use the stack with the designated register and advance the \code{pc}:
\begin{scheme}
  (define (make-save inst machine stack pc)
    (let ((reg (get-register machine
                             (stack-inst-reg-name inst))))
      (lambda ()
        (push stack (get-contents reg))
        (advance-pc pc))))

  (define (make-restore inst machine stack pc)
    (let ((reg (get-register machine
                             (stack-inst-reg-name inst))))
      (lambda ()
        (set-contents! reg (pop stack))
        (advance-pc pc))))

  (define (stack-inst-reg-name stack-instruction)
    (cadr stack-instruction))
\end{scheme}

The final instruction type, handled by \code{make-perform}, generates an execution procedure for the action to be performed.
At simulation time, the action procedure is executed and the \code{pc} advanced.

\begin{scheme}
  (define (make-perform inst machine labels operations pc)
    (let ((action (perform-action inst)))
      (if (operation-exp? action)
          (let ((action-proc
                 (make-operation-exp
                  action machine labels operations)))
            (lambda () (action-proc) (advance-pc pc)))
          (error "Bad PERFORM instruction: ASSEMBLE" inst))))
  (define (perform-action inst) (cdr inst))
\end{scheme}



\subsubsection*{Execution procedures for subexpressions}

The value of a \code{reg}, \code{label}, or \code{const} expression may be needed for assignment to a register (\code{make-assign}) or for input to an operation (\code{make-operation-exp}, below).
The following procedure generates execution procedures to produce values for these expressions during the simulation:
\begin{scheme}
  (define (make-primitive-exp exp machine labels)
    (cond ((constant-exp? exp)
           (let ((c (constant-exp-value exp)))
             (lambda () c)))
          ((label-exp? exp)
           (let ((insts (lookup-label
                         labels
                         (label-exp-label exp))))
             (lambda () insts)))
          ((register-exp? exp)
           (let ((r (get-register machine (register-exp-reg exp))))
             (lambda () (get-contents r))))
          (else (error "Unknown expression type: ASSEMBLE" exp))))
\end{scheme}
The syntax of \code{reg}, \code{label}, and \code{const} expressions is determined by
\begin{scheme}
  (define (register-exp? exp) (tagged-list? exp 'reg))

  (define (register-exp-reg exp) (cadr exp))

  (define (constant-exp? exp) (tagged-list? exp 'const))

  (define (constant-exp-value exp) (cadr exp))

  (define (label-exp? exp) (tagged-list? exp 'label))

  (define (label-exp-label exp) (cadr exp))
\end{scheme}

\code{assign}, \code{perform}, and \code{test} instructions may include the application of a machine operation (specified by an \code{op} expression) to some operands (specified by \code{reg} and \code{const} expressions).
The following procedure produces an execution procedure for an “operation expression”---a list containing the operation and operand expressions from the instruction:
\begin{scheme}
  (define (make-operation-exp exp machine labels operations)
    (let ((op (lookup-prim (operation-exp-op exp)
                           operations))
          (aprocs
           (map (lambda (e)
                  (make-primitive-exp e machine labels))
                (operation-exp-operands exp))))
      (lambda ()
        (apply op (map (lambda (p) (p)) aprocs)))))
\end{scheme}
The syntax of operation expressions is determined by
\begin{scheme}
  (define (operation-exp? exp)
    (and (pair? exp) (tagged-list? (car exp) 'op)))

  (define (operation-exp-op operation-exp)
    (cadr (car operation-exp)))

  (define (operation-exp-operands operation-exp)
    (cdr operation-exp))
\end{scheme}

Observe that the treatment of operation expressions is very much like the treatment of procedure applications by the \code{analyze-application} procedure in the evaluator of \link{Section 4.1.7} in that we generate an execution procedure for each operand.
At simulation time, we call the operand procedures and apply the Scheme procedure that simulates the operation to the resulting values.
The simulation procedure is found by looking up the operation name in the operation table for the machine:
\begin{scheme}
  (define (lookup-prim symbol operations)
    (let ((val (assoc symbol operations)))
      (if val
          (cadr val)
          (error "Unknown operation: ASSEMBLE"
                 symbol))))
\end{scheme}



\begin{exercise}
	\label{Exercise 5.9}
	The treatment of machine operations above permits them to operate on labels as well as on constants and the contents of registers.
	Modify the expression-processing procedures to enforce the condition that operations can be used only with registers and constants.
\end{exercise}



\begin{exercise}
	\label{Exercise 5.10}
	Design a new syntax for register-machine instructions and modify the simulator to use your new syntax.
	Can you implement your new syntax without changing any part of the simulator except the syntax procedures in this section?
\end{exercise}



\begin{exercise}
	\label{Exercise 5.11}
	When we introduced \code{save} and \code{restore} in \link{Section 5.1.4}, we didn’t specify what would happen if you tried to restore a register that was not the last one saved, as in the sequence
	\begin{scheme}
	  (save y)  (save x)  (restore y)
	\end{scheme}
	There are several reasonable possibilities for the meaning of \code{restore}:
	\begin{enumerate}[label = \alph*., leftmargin = *]

		\item
			\code{(restore y)} puts into \code{y} the last value saved on the stack, regardless of what register that value came from.
			This is the way our simulator behaves.
			Show how to take advantage of this behavior to eliminate one instruction from the Fibonacci machine of \link{Section 5.1.4} (\link{Figure 5.12}).

		\item
			\code{(restore y)} puts into \code{y} the last value saved on the stack, but only if that value was saved from \code{y};
			otherwise, it signals an error.
			Modify the simulator to behave this way.
			You will have to change \code{save}  to put the register name on the stack along with the value.

		\item
			\code{(restore y)} puts into \code{y} the last value saved from \code{y} regardless of what other registers were saved after \code{y} and not restored.
			Modify the simulator to behave this way.
			You will have to associate a separate stack with each register.
			You should make the \code{initialize-stack} operation initialize all the register stacks.

	\end{enumerate}
\end{exercise}



\begin{exercise}
	\label{Exercise 5.12}
	The simulator can be used to help determine the data paths required for implementing a machine with a given controller.
	Extend the assembler to store the following information in the machine model:
	\begin{itemize}

		\item
			a list of all instructions, with duplicates removed, sorted by instruction type (\code{assign}, \code{goto}, and so on);

		\item
			a list (without duplicates) of the registers used to hold entry points (these are the registers referenced by \code{goto} instructions);

		\item
			a list (without duplicates) of the registers that are \code{save}d or \code{restore}d;

		\item
			for each register, a list (without duplicates) of the sources from which it is assigned (for example, the sources for register \code{val} in the factorial machine of \link{Figure 5.11} are \code{(const 1)} and \code{((op *) (reg n) (reg val))}).

	\end{itemize}
	Extend the message-passing interface to the machine to provide access to this new information.
	To test your analyzer, define the Fibonacci machine from \link{Figure 5.12} and examine the lists you constructed.
\end{exercise}



\begin{exercise}
	\label{Exercise 5.13}
	Modify the simulator so that it uses the controller sequence to determine what registers the machine has rather than requiring a list of registers as an argument to \code{make-machine}.
	Instead of pre-allocating the registers in \code{make-machine}, you can allocate them one at a time when they are first seen during assembly of the instructions.
\end{exercise}



\subsection{Monitoring Machine Performance}
\label{Section 5.2.4}

Simulation is useful not only for verifying the correctness of a proposed machine design but also for measuring the machine’s performance.
For example, we can install in our simulation program a “meter” that measures the number of stack operations used in a computation.
To do this, we modify our simulated stack to keep track of the number of times registers are saved on the stack and the maximum depth reached by the stack, and add a message to the stack’s interface that prints the statistics, as shown below.
We also add an operation to the basic machine model to print the stack statistics, by initializing \code{the-ops} in \code{make-new-machine} to
\begin{scheme}
  (list (list 'initialize-stack
              (lambda () (stack 'initialize)))
        (list 'print-stack-statistics
              (lambda () (stack 'print-statistics))))
\end{scheme}
Here is the new version of \code{make-stack}:
\begin{scheme}
  (define (make-stack)
    (let ((s '())
          (number-pushes 0)
          (max-depth 0)
          (current-depth 0))
      (define (push x)
        (set! s (cons x s))
        (set! number-pushes (+ 1 number-pushes))
        (set! current-depth (+ 1 current-depth))
        (set! max-depth (max current-depth max-depth)))
      (define (pop)
        (if (null? s)
            (error "Empty stack: POP")
            (let ((top (car s)))
              (set! s (cdr s))
              (set! current-depth (- current-depth 1))
              top)))
      (define (initialize)
        (set! s '())
        (set! number-pushes 0)
        (set! max-depth 0)
        (set! current-depth 0)
        'done)
      (define (print-statistics)
        (newline)
        (display (list 'total-pushes  '= number-pushes
                       'maximum-depth '= max-depth)))
      (define (dispatch message)
        (cond ((eq? message 'push) push)
              ((eq? message 'pop) (pop))
              ((eq? message 'initialize) (initialize))
              ((eq? message 'print-statistics)
               (print-statistics))
              (else (error "Unknown request: STACK" message))))
      dispatch))
\end{scheme}

\link{Exercise 5.15} through \link{Exercise 5.19} describe other useful monitoring and debugging features that can be added to the register-machine simulator.



\begin{exercise}
	\label{Exercise 5.14}
	Measure the number of pushes and the maximum stack depth required to compute \( n!
	\) for various small values of \( n \) using the factorial machine shown in \link{Figure 5.11}.
	From your data determine formulas in terms of \( n \) for the total number of push operations and the maximum stack depth used in computing \( n!
	\) for any \( n > 1 \).
	Note that each of these is a linear function of \( n \) and is thus determined by two constants.
	In order to get the statistics printed, you will have to augment the factorial machine with instructions to initialize the stack and print the statistics.
	You may want to also modify the machine so that it repeatedly reads a value for \( n \), computes the factorial, and prints the result (as we did for the \acronym{GCD} machine in \link{Figure 5.4}), so that you will not have to repeatedly invoke \code{get-register-contents}, \code{set-register-contents!}, and \code{start}.
\end{exercise}



\begin{exercise}
	\label{Exercise 5.15}
	Add \newterm{instruction counting} to the register machine simulation.
	That is, have the machine model keep track of the number of instructions executed.
	Extend the machine model’s interface to accept a new message that prints the value of the instruction count and resets the count to zero.
\end{exercise}



\begin{exercise}
	\label{Exercise 5.16}
	Augment the simulator to provide for \newterm{instruction tracing}.
	That is, before each instruction is executed, the simulator should print the text of the instruction.
	Make the machine model accept \code{trace-on} and \code{trace-off} messages to turn tracing on and off.
\end{exercise}



\begin{exercise}
	\label{Exercise 5.17}
	Extend the instruction tracing of \link{Exercise 5.16} so that before printing an instruction, the simulator prints any labels that immediately precede that instruction in the controller sequence.
	Be careful to do this in a way that does not interfere with instruction counting (\link{Exercise 5.15}).
	You will have to make the simulator retain the necessary label information.
\end{exercise}



\begin{exercise}
	\label{Exercise 5.18}
	Modify the \code{make-register} procedure of \link{Section 5.2.1} so that registers can be traced.
	Registers should accept messages that turn tracing on and off.
	When a register is traced, assigning a value to the register should print the name of the register, the old contents of the register, and the new contents being assigned.
	Extend the interface to the machine model to permit you to turn tracing on and off for designated machine registers.
\end{exercise}



\begin{exercise}
	\label{Exercise 5.19}
	Alyssa P. Hacker wants a \newterm{breakpoint} feature in the simulator to help her debug her machine designs.
	You have been hired to install this feature for her.
	She wants to be able to specify a place in the controller sequence where the simulator will stop and allow her to examine the state of the machine.
	You are to implement a procedure
	\begin{scheme}
	  (set-breakpoint ⟨~\var{machine}~⟩ ⟨~\var{label}~⟩ ⟨~\var{n}~⟩)
	\end{scheme}
	that sets a breakpoint just before the \( n \)\textsuperscript{th} instruction after the given label.
	For example,
	\begin{scheme}
	  (set-breakpoint gcd-machine 'test-b 4)
	\end{scheme}
	installs a breakpoint in \code{gcd-machine} just before the assignment to register \code{a}.
	When the simulator reaches the breakpoint it should print the label and the offset of the breakpoint and stop executing instructions.
	Alyssa can then use \code{get-register-contents} and \code{set-register-contents!} to manipulate the state of the simulated machine.
	She should then be able to continue execution by saying
	\begin{scheme}
	  (proceed-machine ⟨~\var{machine}~⟩)
	\end{scheme}
	She should also be able to remove a specific breakpoint by means of
	\begin{scheme}
	  (cancel-breakpoint ⟨~\var{machine}~⟩ ⟨~\var{label}~⟩ ⟨~\var{n}~⟩)
	\end{scheme}
	or to remove all breakpoints by means of
	\begin{scheme}
	  (cancel-all-breakpoints ⟨~\var{machine}~⟩)
	\end{scheme}
\end{exercise}
